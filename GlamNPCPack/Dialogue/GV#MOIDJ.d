BEGIN GV#MOIDJ

//Silke
I_C_T ~%tutu_var%SILKE~ 6 GV#MOIDInterjectSILKE1
== GV#MOIDJ IF ~InParty("GV#MOID") InMyArea("GV#MOID") !StateCheck("GV#MOID",CD_STATE_NOTVALID)~ THEN
~Well isn't this just the story of my life.~
END

//Hurgan
I_C_T ~%tutu_var%HURGAN~ 4 GV#MOIDInterjectHURGAN1
== GV#MOIDJ IF ~InParty("GV#MOID") InMyArea("GV#MOID") !StateCheck("GV#MOID",CD_STATE_NOTVALID)~ THEN
~Know the name. Know the tower too. Hope this isn't going where I think it is.~
END

I_C_T ~%tutu_var%HURGAN~ 7 GV#MOIDInterjectHURGAN2
== GV#MOIDJ IF ~InParty("GV#MOID") InMyArea("GV#MOID") !StateCheck("GV#MOID",CD_STATE_NOTVALID)~ THEN
~Any answer but "Gods no" and you'll be making my job real difficult.~
END

//Jenkal
I_C_T2 ~%tutu_var%JENKAL~ 0 GV#MOIDInterjectJENKAL1
== GV#MOIDJ IF ~InParty("GV#MOID") InMyArea("GV#MOID") !StateCheck("GV#MOID",CD_STATE_NOTVALID)~ THEN
~Least somebody here has some sense.~
END

//Kaishas
I_C_T2 ~%tutu_var%KAISH~ 13 GV#MOIDInterjectKAISH1
== GV#MOIDJ IF ~InParty("GV#MOID") InMyArea("GV#MOID") !StateCheck("GV#MOID",CD_STATE_NOTVALID)~ THEN
~Don't be too eager to escape. Place like this, deaf to the world's troubles...it's not the prison you think it is.~
END

//Captain Tollar
I_C_T2 ~%tutu_var%KIERES~ 7 GV#MOIDInterjectKIERES1
== GV#MOIDJ IF ~InParty("GV#MOID") InMyArea("GV#MOID") !StateCheck("GV#MOID",CD_STATE_NOTVALID)~ THEN
~Huh. Either we've met a lightweight sailor or that's one hell of a brew. Might have to pick some up myself. For research.~
END

//Ghost Knights
I_C_T2 ~%GhostKnight%~ 0 GV#MOIDInterjectKNIGHT1
== GV#MOIDJ IF ~InParty("GV#MOID") InMyArea("GV#MOID") !StateCheck("GV#MOID",CD_STATE_NOTVALID)~ THEN
~There's the power of an oath for you. Simple words, but it keeps them anchored, spitting in death's face.~
END

I_C_T2 ~%GhostKnight%~ 1 GV#MOIDInterjectKNIGHT2
== GV#MOIDJ IF ~InParty("GV#MOID") InMyArea("GV#MOID") !StateCheck("GV#MOID",CD_STATE_NOTVALID)~ THEN
~Tan am sonn.~
END

//Ted
I_C_T2 ~%tutu_var%HUNTER~ 1 GV#MOIDInterjectHUNTER1
== GV#MOIDJ IF ~InParty("GV#MOID") InMyArea("GV#MOID") !StateCheck("GV#MOID",CD_STATE_NOTVALID)~ THEN
~Sane people quit after something like that. Explains the shortage of sane adventurers.~
END

//Lamalha
I_C_T2 ~%tutu_var%LAMALH~ 0 GV#MOIDInterjectLAMALH1
== GV#MOIDJ IF ~InParty("GV#MOID") InMyArea("GV#MOID") !StateCheck("GV#MOID",CD_STATE_NOTVALID)~ THEN
~Somehow got the feeling it doesn't.~
END

//Laskal
I_C_T2 ~%tutu_var%LASKAL~ 3 GV#MOIDInterjectLASKAL1
== GV#MOIDJ IF ~InParty("GV#MOID") InMyArea("GV#MOID") !StateCheck("GV#MOID",CD_STATE_NOTVALID)~ THEN
~Doesn't look like a courier to me. Answer carefully.~
END

//Lussel
I_C_T2 ~%tutu_var%LUSSEL~ 2 GV#MOIDInterjectLUSSEL1
== GV#MOIDJ IF ~InParty("GV#MOID") InMyArea("GV#MOID") !StateCheck("GV#MOID",CD_STATE_NOTVALID)~ THEN
~Ooze booze? Knew humans couldn't brew worth a damn, but I didn't realise it was this bad.~
END

//Marek
I_C_T2 ~%tutu_var%MAREK~ 3 GV#MOIDInterjectMAREK1
== GV#MOIDJ IF ~InParty("GV#MOID") InMyArea("GV#MOID") !StateCheck("GV#MOID",CD_STATE_NOTVALID)~ THEN
~Heard it before, every which way, from worse and from better. Still kicking.~
END

//Meilum
I_C_T2 ~%tutu_var%MEILUM~ 0 GV#MOIDInterjectMEILUM1
== GV#MOIDJ IF ~InParty("GV#MOID") InMyArea("GV#MOID") !StateCheck("GV#MOID",CD_STATE_NOTVALID)~ THEN
~If you have to introduce yourself as the Coast's greatest swordsman, you aren't.~
END

//Molkar
I_C_T2 ~%tutu_var%MOLKAR~ 0 GV#MOIDInterjectMOLKAR1
== GV#MOIDJ IF ~InParty("GV#MOID") InMyArea("GV#MOID") !StateCheck("GV#MOID",CD_STATE_NOTVALID)~ THEN
~Strange how they all say that. A "competent" bounty hunter would notice the pattern.~
END

//Neira
I_C_T ~%tutu_var%NEIRA~ 0 GV#MOIDInterjectNEIRA1
== GV#MOIDJ IF ~InParty("GV#MOID") InMyArea("GV#MOID") !StateCheck("GV#MOID",CD_STATE_NOTVALID)~ THEN
~Might've been easy coins waiting for us to sleep. Now I don't like your chances.~
END

//Neville
I_C_T ~%tutu_var%NEVILL~ 0 GV#MOIDInterjectNEVILLE1
== GV#MOIDJ IF ~InParty("GV#MOID") InMyArea("GV#MOID") !StateCheck("GV#MOID",CD_STATE_NOTVALID)~ THEN
~Grab some shade. Archers lurking.~
END

//Niklos
I_C_T ~%tutu_var%NIKLOS~ 1 GV#MOIDInterjectNIKLOS1
== GV#MOIDJ IF ~InParty("GV#MOID") InMyArea("GV#MOID") !StateCheck("GV#MOID",CD_STATE_NOTVALID)~ THEN
~Careful. Someone pays gold just to talk to you, they mean to get it back. ~
END

//Scar
I_C_T ~%tutu_var%SCAR~ 2 GV#MOIDInterjectSCAR1
== GV#MOIDJ IF ~InParty("GV#MOID") InMyArea("GV#MOID") !StateCheck("GV#MOID",CD_STATE_NOTVALID)~ THEN
~Only capacity I ever work in.~
END

//Dorotea


//Stephan
I_C_T2 ~%tutu_var%STEPHA~ 4 GV#MOIDInterjectSTEPHAN1
== GV#MOIDJ IF ~InParty("GV#MOID") InMyArea("GV#MOID") !StateCheck("GV#MOID",CD_STATE_NOTVALID)~ THEN
~Easiest interrogation I've ever seen. If everyone folded that quick, there'd be a lot less busted knees in the world.~
END

//Surgeon
I_C_T2 ~%tutu_var%SURGEO~ 5 GV#MOIDInterjectSURGEON1
== GV#MOIDJ IF ~InParty("GV#MOID") InMyArea("GV#MOID") !StateCheck("GV#MOID",CD_STATE_NOTVALID)~ THEN
~Healing in penance. Might've done the same myself, on better terms with the gods.~
END

//Tamoko
I_C_T2 ~%tutu_var%TAMOKO~ 10 GV#MOIDInterjectTAMOKO1
== GV#MOIDJ IF ~InParty("GV#MOID") InMyArea("GV#MOID") !StateCheck("GV#MOID",CD_STATE_NOTVALID)~ THEN
~Don't promise before you know. This is not a road you want to walk.~
END

//Alatos
I_C_T ~%tutu_var%ALATOS~ 0 GV#MOIDInterjectALATOS1
== GV#MOIDJ IF ~InParty("GV#MOID") InMyArea("GV#MOID") !StateCheck("GV#MOID",CD_STATE_NOTVALID)~ THEN
~Exactly what someone would say before commencing hostilities.~
END

//Tarnesh
I_C_T ~%tutu_var%TARNES~ 0 GV#MOIDInterjectTARNESH1
== GV#MOIDJ IF ~InParty("GV#MOID") InMyArea("GV#MOID") !StateCheck("GV#MOID",CD_STATE_NOTVALID)~ THEN
~Guard up. This one's too chummy.~
END

//Nalin
I_C_T2 ~%tutu_var%NALIN~ 4 GV#MOIDInterjectNALIN1
== GV#MOIDJ IF ~InParty("GV#MOID") InMyArea("GV#MOID") !StateCheck("GV#MOID",CD_STATE_NOTVALID)~ THEN
~Gallows would've been kinder to him. You don't come back from something like this.~
END

//Wenric
I_C_T2 ~%tutu_var%WENRIC~ 1 GV#MOIDInterjectWENRIC1
== GV#MOIDJ IF ~InParty("GV#MOID") InMyArea("GV#MOID") !StateCheck("GV#MOID",CD_STATE_NOTVALID)~ THEN
~Bag twenty corpses, keep my own count, 150 gold. All for a peaceful night. Wish more clients were this simple.~
END



APPEND GV#MOIDJ


//Friendship Track

//Talk 1

IF ~Global("GV#MOIDTalk","GLOBAL",2)~ t
SAY ~Big job, guarding you. Wonder sometimes if I'm up to it.~
++ ~You've done fine work so far.~ + t.1
++ ~So do I.~ + t.2
++ ~Surely you've had more important clients than me.~ + t.3
++ ~Didn't I say I don't need protection? You're just here so we can make shorter work of my enemies.~ + t.4
++ ~It'd be a smaller job with less talk. Be quiet.~ DO ~IncrementGlobal("GV#MOIDTalk","GLOBAL",1) SetGlobal("GV#MOIDShutUp","GLOBAL",1)~ EXIT
END

IF ~~ t.1
SAY ~"So far" doesn't mean anything. You only did fine when it's over.~
IF ~~ + t.5
END

IF ~~ t.2
SAY ~Good thing you didn't pay in advance. Or at all, really.~
IF ~~ + t.5
END

IF ~~ t.3
SAY ~Might've done. Might've wondered then, too.~
IF ~~ + t.5
END

IF ~~ t.4
SAY ~That attitude's what makes it a big job.~
IF ~~ + t.5
END

IF ~~ t.5
SAY ~Been working one-offs for a while now. Smugglers. Cons who con the wrong people. You want to keep your business dark, you don't hire long-term. This is a jump.~
++ ~You take jobs with criminals?~ + t.6
++ ~You could do better than that.~ + t.7
++ ~I hadn't pegged you for the type.~ + t.8
++ ~Shady dealings, eh? I knew there was more to you.~ + t.9
END

IF ~~ t.6
SAY ~They don't say they're criminals when they hire me.~
IF ~~ + t.10
END

IF ~~ t.7
SAY ~No I can't. Tried it.~
IF ~~ + t.10
END

IF ~~ t.8
SAY ~Only thing I'm good at. Not much room to fuss.~
IF ~~ + t.10
END

IF ~~ t.9
SAY ~There isn't. I don't get into devilry. I just take lumps for those who do.~
IF ~~ + t.10
END

IF ~~ t.10
SAY ~What did you expect? People who deserve bodyguards don't pick them up in taverns.~
+ ~ReputationGT(Player1,12)~ + ~I picked you up in a tavern.~ + t.11
+ ~ReputationLT(Player1,13)~ + ~I picked you up in a tavern.~ + t.12
++ ~Don't offer your services in taverns, then!~ + t.13
++ ~I resent that.~ + t.14
++ ~Then why not stop working for them?~ + t.15
END

IF ~~ t.11
SAY ~Bad people put up good fronts.~
IF ~~ DO ~IncrementGlobal("GV#MOIDTalk","GLOBAL",1) RealSetGlobalTimer("GV#MOIDTimer","GLOBAL",3600) ~ EXIT
END

IF ~~ t.12
SAY ~And what? You're an exception? Haven't done much to convince me.~
IF ~~ DO ~IncrementGlobal("GV#MOIDTalk","GLOBAL",1) RealSetGlobalTimer("GV#MOIDTimer","GLOBAL",3600) ~ EXIT
END

IF ~~ t.13
SAY ~I'll rephrase. People who deserve bodyguards don't pick me.~
IF ~~ DO ~IncrementGlobal("GV#MOIDTalk","GLOBAL",1) RealSetGlobalTimer("GV#MOIDTimer","GLOBAL",3600) ~ EXIT
END

IF ~~ t.14
SAY ~Resent away. Truth's truth.~
IF ~~ DO ~IncrementGlobal("GV#MOIDTalk","GLOBAL",1) RealSetGlobalTimer("GV#MOIDTimer","GLOBAL",3600) ~ EXIT
END


IF ~~ t.15
SAY ~And starve to death? Thought about it. Still do.~
IF ~~ DO ~IncrementGlobal("GV#MOIDTalk","GLOBAL",1) RealSetGlobalTimer("GV#MOIDTimer","GLOBAL",3600) ~ EXIT
END


//Talk 2

IF ~Global("GV#MOIDTalk","GLOBAL",4)~ t1
SAY ~Someone teach you to fight, or did you just pick it up as you went?~
+ ~!Class(Player1,FIGHTER_ALL) !Class(Player1,RANGER_ALL) !Class(Player1,PALADIN_ALL)~ + ~I wouldn't exactly call myself a fighter.~ + t1.1
++ ~I took lessons with the guards in Candlekeep.~ + t1.2
++ ~The latter. I've had to pick lots of things up lately.~ + t1.3
++ ~Neither. It's just a gift.~ + t1.4
END

IF ~~ t1.1
SAY ~More to it than hitting people. Acting fast, keeping your wits, positioning yourself. Takes a fighter's brain to handle that. ~
IF ~~ + t1.5
END

IF ~~ t1.2
SAY ~Think you mostly picked it up, then. No offence to them, but I doubt it takes much to guard books.~
IF ~~ + t1.5
END

IF ~~ t1.3
SAY ~Sink or swim. Best way. No substitute for the real thing, risk aside.~
IF ~~ + t1.5
END

IF ~~ t1.4
SAY ~Raw talent, is it? Had me fooled. Or you're just full of crap.~
IF ~~ + t1.5
END

IF ~~ t1.5
SAY ~Not commenting on your skills, mind. Just wondering where normal people learn to do this.~
++ ~What do you mean?~ + t1.6
++ ~I'm hardly normal, Moidre.~ + t1.6
++ ~So you're an abnormal person?~ + t1.6
END

IF ~~ t1.6
SAY ~You're not a soldier. That's what I mean. You weren't...~
= ~You're not a soldier.~
++ ~Are you?~ + t1.7
++ ~I wasn't what?~ + t1.7
++ ~Soldiers aren't the only ones who can kill.~ + t1.7
END

IF ~~ t1.7
SAY ~Never mind. Shouldn't be chatting anyway. Ignore me.~
IF ~~ DO ~IncrementGlobal("GV#MOIDTalk","GLOBAL",1) RealSetGlobalTimer("GV#MOIDTimer","GLOBAL",3600) ~ EXIT
END



// Talk 3

IF ~Global("GV#MOIDTalk","GLOBAL",6)~ t2
SAY ~Zzz...~
= ~Hng...huh?!~
++ ~Moidre? ~ + t2.1
++ ~Keep it down over there!~ + t2.1
++ ~Did you see something?~ + t2.1
END

IF ~~ t2.1
SAY ~Oh. Sorry. Fell asleep.~
++ ~Why are you sorry? We're supposed to be sleeping.~ + t2.2
++ ~Still standing guard, are you?~ + t2.3
++ ~Don't let it happen again.~ + t2.4
++ ~Are you saying you don't sleep?~ + t2.5
END

IF ~~ t2.2
SAY ~I'm not.~
IF ~~ + t2.6
END

IF ~~ t2.3
SAY ~Maybe not "standing" guard.~
IF ~~ + t2.6
END

IF ~~ t2.4
SAY ~Do my best.~
IF ~~ + t2.6
END

IF ~~ t2.5
SAY ~Not on purpose. Not when I'm working.~
IF ~~ + t2.6
END

IF ~~ t2.6
SAY ~You've done some tossing and turning yourself lately. Nightmares?~
++ ~Yes. About my foster father's death and...something else. A voice.~ + t2.7
++ ~Not exactly. Just strange dreams. Vivid ones.~ + t2.8
+ ~!Race(Player1,ELF)~ + ~No. I'm just a restless sleeper.~ + t2.9
+ ~Race(Player1,ELF)~ + ~No. Some elves are just restless during trances.~ + t2.9
++ ~Goodnight, Moidre.~ DO ~IncrementGlobal("GV#MOIDTalk","GLOBAL",1) RealSetGlobalTimer("GV#MOIDTimer","GLOBAL",3600) RestParty()~ EXIT
END

IF ~~ t2.7
SAY ~Wouldn't worry. Someone close dies, it's natural they'll linger. And hearing voices is only madness when you're awake.~
IF ~~ + t2.10
END

IF ~~ t2.8
SAY ~Better strange than grim. Wouldn't fault you for sleeping the day away, if strange is the worst you can say.~
IF ~~ + t2.10
END

IF ~~ t2.9
SAY ~Restless is right. Roll around so much I near have to drag you back to camp.~
IF ~~ + t2.10
END

IF ~~ t2.10
SAY ~I used to have dreams. Proper dreams, the sort you'd tell people about. Can't remember them anymore, but I know I had them.~
= ~These days I just dream of work. Go to sleep, do a day's toil in my head, wake up ready for bed. Don't know why I bother.~
++ ~Sounds like you're tired of your job. Why not do something else?~ + t2.11
++ ~Bother sleeping, or bother in general?~ + t2.12
++ ~I wouldn't rest either if I didn't have to.~  + t2.13
++ ~I know why I bother. Goodnight.~ DO ~IncrementGlobal("GV#MOIDTalk","GLOBAL",1) RealSetGlobalTimer("GV#MOIDTimer","GLOBAL",3600) RestParty()~ EXIT
END

IF ~~ t2.11
SAY ~Ever get tired of adventuring?~
++ ~Yes.~ + t2.14
++ ~No.~ + t2.15
END

IF ~~ t2.12
SAY ~Either.~
IF ~~ + t2.16
END

IF ~~ t2.13
SAY ~Gods' way of keeping us in check. Imagine how fast we could ruin the world if we didn't have to stop and lie down sometimes.~
IF ~~ + t2.16
END

IF ~~ t2.14
SAY ~But you're still doing it.~
IF ~~ + t2.16
END

IF ~~ t2.15
SAY ~You wouldn't understand, then.~
IF ~~ + t2.16
END

IF ~~ t2.16
SAY ~Anyway. Up now. Think I'll just stay up. Sleep safe.~
IF ~~ DO ~IncrementGlobal("GV#MOIDTalk","GLOBAL",1) RealSetGlobalTimer("GV#MOIDTImer","GLOBAL",3600) RestParty()~ EXIT
END


// Talk 4


IF ~Global("GV#MOIDTalk","GLOBAL",8)~ t3
SAY ~(Let me suffer where you would have suffered. Let me bleed where you would have bled. Let me die where you would have died. My shield is my self...)~
++ ~What are you doing?~ + t3.1
++ ~(ignore her)~ DO ~IncrementGlobal("GV#MOIDTalk","GLOBAL",1) RealSetGlobalTimer("GV#MOIDTImer","GLOBAL",3600)~ EXIT
END

IF ~~ t3.1
SAY ~The oath. Have to recite it every day, even just to myself. Suppose you were bound to notice eventually.~
++ ~You "have" to recite it?~ + t3.2
++ ~That doesn't sound like mercenary training to me.~ + t3.3
++ ~Where did that oath come from? Surely you didn't just make it up.~ + t3.4
END

IF ~~ t3.2
SAY ~Well. Don't *have* to. But I do.~
IF ~~ + t3.5
END

IF ~~ t3.3
SAY ~Know a lot of mercenaries?~
IF ~~ + t3.5
END

IF ~~ t3.4
SAY ~Why not? Someone has to make them up.~
IF ~~ + t3.5
END

IF ~~ t3.5
SAY ~It's difficult, the world. Everyone's uncertain sometimes. Some more than others. Helps to have something to go by when hesitation can kill you.~
++ ~Most people would go by their morals.~ + t3.6
++ ~You mustn't have much confidence in it, if you have to keep repeating it to yourself.~ + t3.7
++ ~I say a person should trust their instincts.~ + t3.8
++ ~I've never hesitated in my life.~ + t3.9
END

IF ~~ t3.6
SAY ~Morals need a foundation. Otherwise they're just feelings.~
IF ~~ + t3.10
END

IF ~~ t3.7
SAY ~Priests say their prayers every day. Doesn't mean they don't believe.~
IF ~~ + t3.10
END

IF ~~ t3.8
SAY ~Instinct tells you to flinch when you should follow through. Instinct tells you to rest when you need to press on. It's the last thing a person should trust.~
IF ~~ + t3.10
END

IF ~~ t3.9
SAY ~Live longer. Then that'll be something to boast about.~
IF ~~ + t3.10
END

IF ~~ t3.10
SAY ~Tan am sonn. "Be as stone." Oldest dwarven saying. Not just a saying; a fond wish, a farewell. A battlecry and a wedding vow. A drinking song and a hymn. It's everything.~
= ~Oaths are stone. Keep to them, and they'll brace you against the world. Walk away and they'll loom at your back forever. But they stand, with or without you.~
++ ~So as long as you keep your oaths, no matter what it takes, it means you're a good person?~ + t3.11
++ ~I agree. A person's word is the only thing that matters.~ + t3.12
++ ~I'd sooner betray an oath than betray myself.~ + t3.13
++ ~What a load of nonsense.~ + t3.14
END

IF ~~ t3.11
SAY ~It means you're stone. Sometimes that's all a person can be.~
IF ~~ DO ~IncrementGlobal("GV#MOIDTalk","GLOBAL",1) RealSetGlobalTimer("GV#MOIDTImer","GLOBAL",3600)~ EXIT
END

IF ~~ t3.12
SAY ~Then you don't agree, because that's not what I said.~
IF ~~ DO ~IncrementGlobal("GV#MOIDTalk","GLOBAL",1) RealSetGlobalTimer("GV#MOIDTImer","GLOBAL",3600)~ EXIT
END

IF ~~ t3.13
SAY ~You decided you're worth more. Some people didn't.~
IF ~~ DO ~IncrementGlobal("GV#MOIDTalk","GLOBAL",1) RealSetGlobalTimer("GV#MOIDTImer","GLOBAL",3600)~ EXIT
END

IF ~~ t3.14
SAY ~Maybe. Best I've got, though.~
IF ~~ DO ~IncrementGlobal("GV#MOIDTalk","GLOBAL",1) RealSetGlobalTimer("GV#MOIDTImer","GLOBAL",3600)~ EXIT
END
END


//Talk 5
CHAIN IF ~Global("GV#MOIDTalk","GLOBAL",10) Global("GV#MOIDHappy","GLOBAL",1)~ THEN GV#MOIDJ t4
~What's that human saying? "History repeats itself"? It really does.~
== GV#MOIDJ IF ~GlobalLT("CHAPTER","GLOBAL",%tutu_chapter_7%)~ THEN ~Everyone in Baldur's Gate thinks this Rieltar is their saviour. Swooping in with his iron when all hope seemed lost, arming them for a war they'd have died in otherwise.~
== GV#MOIDJ IF ~GlobalGT("CHAPTER","GLOBAL",%tutu_chapter_6%)~ THEN ~Everyone in Baldur's Gate thinks Sarevok is their saviour. Swooping in with his iron when all hope seemed lost, arming them for a war they'd have died in otherwise.~
== GV#MOIDJ ~Every one of them is wrong. And if I told them, not a single one would listen.~
END
+ ~GlobalLT("CHAPTER","GLOBAL",%tutu_chapter_7%)~ + ~And that's history repeating itself?~ EXTERN GV#MOIDJ t4.1
+ ~GlobalGT("CHAPTER","GLOBAL",%tutu_chapter_6%)~ + ~And that's history repeating itself?~ EXTERN GV#MOIDJ t4.2
+ ~GlobalLT("CHAPTER","GLOBAL",%tutu_chapter_7%)~ + ~I'm not sure what you mean.~ EXTERN GV#MOIDJ t4.3
+ ~GlobalGT("CHAPTER","GLOBAL",%tutu_chapter_6%)~ + ~I'm not sure what you mean.~ EXTERN GV#MOIDJ t4.4
+ ~GlobalLT("CHAPTER","GLOBAL",%tutu_chapter_7%)~ + ~Don't be too upset. They wouldn't listen to me either.~ EXTERN GV#MOIDJ t4.3
+ ~GlobalGT("CHAPTER","GLOBAL",%tutu_chapter_6%)~ + ~Don't be too upset. They wouldn't listen to me either.~ EXTERN GV#MOIDJ t4.4


CHAIN GV#MOIDJ t4.1
~For me and for the world. Pick a date, pick a place, somewhere you'll find a Rieltar. Just so happens this is my second.~
EXTERN GV#MOIDJ t4.3

CHAIN GV#MOIDJ t4.2
~For me and for the world. Pick a date, pick a place, somewhere you'll find a Sarevok. Just so happens this is my second.~
EXTERN GV#MOIDJ t4.4

CHAIN GV#MOIDJ t4.3
~You've wondered where I came from? Who I used to work for? Look at Rieltar. There's your answer, more or less.~
END
++ ~You worked for Rieltar?!~ EXTERN GV#MOIDJ t4.5
++ ~Can you elaborate?~ EXTERN GV#MOIDJ t4.6
++ ~So you've been in this position before.~ EXTERN GV#MOIDJ t4.6

CHAIN GV#MOIDJ t4.4
~You've wondered where I came from? Who I used to work for? Look at Sarevok. There's your answer, more or less.~
END
++ ~You worked for Sarevok?!~ EXTERN GV#MOIDJ t4.5
++ ~Can you elaborate?~ EXTERN GV#MOIDJ t4.6
++ ~So you've been in this position before.~ EXTERN GV#MOIDJ t4.6

CHAIN GV#MOIDJ t4.5
~*sigh* You're dense sometimes.~
EXTERN GV#MOIDJ t4.6

CHAIN GV#MOIDJ t4.6
~I was kuldarsamman to my clan leader. "Trusted warrior." Some humans call us dwarven defenders.~
= ~Hand-picked as a child, trained day and night to be a living wall. Body and mind. "My shield is my self, till my days are done."~
END
++ ~From childhood? That sounds like an awful life.~ EXTERN GV#MOIDJ t4.7
++ ~That explains a lot.~ EXTERN GV#MOIDJ t4.7
++ ~Hand-picked as a child? What, did they notice you clubbing the other kids with a stick?~ EXTERN GV#MOIDJ t4.8

CHAIN GV#MOIDJ t4.7
~It's the greatest honour a mortal dwarf can give. Especially for...for me. More than I deserved.~
= ~Never in a million years thought anyone would envy me. Tried to be humble about it, but I'd watch the peasants as we marched from the keep, see the way they looked at me and...~
= ~Well. That's moot. Once you start seeing people as "peasants", it's already gone to your head.~
END
++ ~You mean you had an ego? I'm not sure I believe this story.~ EXTERN GV#MOIDJ t4.9
++ ~I knew you weren't just a mercenary.~ EXTERN GV#MOIDJ t4.10
+ ~GlobalLT("CHAPTER","GLOBAL",%tutu_chapter_7%)~ + ~So how was your clan leader like Rieltar?~ EXTERN GV#MOIDJ t4.11
+ ~GlobalGT("CHAPTER","GLOBAL",%tutu_chapter_6%)~ + ~So how was your clan leader like Sarevok?~ EXTERN GV#MOIDJ t4.11

CHAIN GV#MOIDJ t4.8
~Noticed the other kids clubbing me, maybe. Noticed I didn't flinch much.~
EXTERN GV#MOIDJ t4.7

CHAIN GV#MOIDJ t4.9
~Don't know if I'd go that far. I was proud of my place, not of myself.~
EXTERN GV#MOIDJ t4.12

CHAIN GV#MOIDJ t4.10
~I was, looking back. A glorified one, a well-trained one, but still.~
EXTERN GV#MOIDJ t4.12

CHAIN GV#MOIDJ t4.11
~It's a story. And I'm not much of a teller. Do you really want to hear it?~
END
++ ~I am.~ EXTERN GV#MOIDJ t4.13
++ ~The mysterious Moidre finally wants to shed some light on herself! As if I'd turn that down.~ EXTERN GV#MOIDJ t4.13
++ ~Nope! Just lost all interest.~ DO ~IncrementGlobal("GV#MOIDTalk","GLOBAL",1) SetGlobal("GV#MOIDShutUp","GLOBAL",1)~ EXIT

CHAIN GV#MOIDJ t4.12
~<CHARNAME>, this whole thing, my clan, my home...~
EXTERN GV#MOIDJ t4.11

CHAIN GV#MOIDJ t4.13
~We...the clan...we were recovering from war. Duergar. They lost, but so did we. Thousands dead and nothing to show for it but scars and debt.~
= ~Our old leader died in battle, but his son, the one I served, he was young and bright. Full of ideas. More thinker than fighter, but that was what we needed then. We all believed he'd find us a way through it.~
= ~And he did. Adamantine. Strongest metal in the world, as far as we know. More valuable than gold. Trouble is, most of it's in the Underdark. Can't risk that. Barely enough of us left to mine iron.~
= ~Drow, though. The dark elves. They have plenty. And the ones nearer the surface, the Vhaeraunites, they'll trade when it suits them. Only problem is finding something they want.~
END
++ ~Gold?~ EXTERN GV#MOIDJ t4.14
++ ~Gems?~ EXTERN GV#MOIDJ t4.14
++ ~Weapons?~ EXTERN GV#MOIDJ t4.14
+ ~CheckStatGT(Player1,13,WIS)~ + ~People.~ EXTERN GV#MOIDJ t4.14

CHAIN GV#MOIDJ t4.14
~People.~
= ~Human settlements, sometimes halflings or gnomes. Small ones. We'd come to them in friendship and take them in the night, and there was our adamantine. More than most clans had ever seen. Enough to start rebuilding.~
END
++ ~And you participated in this?~ EXTERN GV#MOIDJ t4.15
++ ~Smart thinking, I say.~ EXTERN GV#MOIDJ t4.16
++ ~That's absolutely despicable.~ EXTERN GV#MOIDJ t4.17

CHAIN GV#MOIDJ t4.15
~...yes.~
EXTERN GV#MOIDJ t4.18

CHAIN GV#MOIDJ t4.16
~That so. Maybe you should wait for the end before you decide.~
EXTERN GV#MOIDJ t4.18

CHAIN GV#MOIDJ t4.17
~Yes. It is. But it worked, at least for a while.~
EXTERN GV#MOIDJ t4.18

CHAIN GV#MOIDJ t4.18
~He used convicts, you see. Long as they did the raids and kept quiet, they'd stay out of prison. But you can't just leave a pack of thieves and murderers to themselves. Someone had to keep them in line.~
= ~"It's them or us," he said. "You're my greatest warrior, my most trusted warrior. Do it for our clan. For me."~
= ~Wasn't so bad at first. I'd march the prisoners home, next day they'd be gone. Easy not to think about it. Problem is, fresh captives don't make good slaves.~
= ~You get a better deal if they're already broken in.~
END
++ ~You tortured people?!~ EXTERN GV#MOIDJ t4.19
++ ~This just keeps getting worse.~ EXTERN GV#MOIDJ t4.19
++ ~This just keeps getting better.~ EXTERN GV#MOIDJ t4.19
++ ~Ho ho! A whole different skill set! I thought you said you were only good at fighting?~ EXTERN GV#MOIDJ t4.19

CHAIN GV#MOIDJ t4.19
~If there's one thing I can say in my defence; and it might be the only thing; it's that I wasn't involved in that part.~
= ~He had others. Professionals. I asked how he found them so quickly, where all the equipment came from, and he just gave me this...this confused little smile. Like it shouldn't even have occurred to me that he didn't already have them.~
= ~That's when I knew. This wasn't just some desperate measure. He wasn't going to stop. He'd always been this way and I never noticed.~
= ~All I ever saw was that proud face and those young, bright eyes. That's all anyone saw. Could've told the whole clan, every last detail, and not one would've believed me. He knew that. That's why he picked me.~
END
++ ~Why wouldn't they believe you specifically?~ EXTERN GV#MOIDJ t4.20
++ ~I can't believe you took part in all this.~ EXTERN GV#MOIDJ t4.21
++ ~I can respect a man like that. Putting himself and his people ahead of everyone else.~ EXTERN GV#MOIDJ t4.22
++ ~Moidre, I can't keep you in my company. Not after hearing what you've done.~ EXTERN GV#MOIDJ t4.23

CHAIN GV#MOIDJ t4.20
~...history. They had reason not to trust me.~
EXTERN GV#MOIDJ t4.24

CHAIN GV#MOIDJ t4.21
~I couldn't either sometimes. But then I'd look at our clanhold and see the state we were in. The squalor. Children with bones showing through their skin. And maybe, just for the time being, we had to do what we could.~
EXTERN GV#MOIDJ t4.24

CHAIN GV#MOIDJ t4.22
~He's dead, <CHARNAME>, and so are his people. It was all for nothing. Does that deserve respect?~
EXTERN GV#MOIDJ t4.24

CHAIN GV#MOIDJ t4.23
~Yeah. I think...I think that might be the reason I told you. Someone like me shouldn't be around someone like you, and it's past time you learned why.~
= ~I swore to protect you, but if you won't have me, all I can do is wish you luck. I hope that's worth something to you. I hope...~
= ~Goodbye, <CHARNAME>.~
DO ~SetGlobal("GV#MOIDJoined","GLOBAL",0) LeaveParty() EscapeArea()~ EXIT

CHAIN GV#MOIDJ t4.24
~My oath was the only thing I had, in the end. Only thing I knew. My shield is my self. And thank the gods for that. Wasn't much of myself left.~
DO ~IncrementGlobal("GV#MOIDTalk","GLOBAL",1) RealSetGlobalTimer("GV#MOIDTImer","GLOBAL",3600)~ EXIT


//Talk 6

CHAIN IF ~Global("GV#MOIDTalk","GLOBAL",12)~ THEN GV#MOIDJ t6
~<CHARNAME>.~
~I'm...~
= ~You know the duergar? The grey dwarves?~
== GV#VYNDJ IF ~IsValidForPartyDialogue("GV#VYND")~ THEN ~Hah, here it comes. I knew it.~
END
++ ~What about them?~ EXTERN GV#MOIDJ t6.1
++ ~No, I don't.~ EXTERN GV#MOIDJ t6.2
++ ~You're one of them?~ EXTERN GV#MOIDJ t6.3


CHAIN GV#MOIDJ t6.1
~I have their blood in me.~
EXTERN GV#MOIDJ t6.4

CHAIN GV#MOIDJ t6.2
~From the Underdark. What drow are to elves, duergar are to dwarves. And...~
EXTERN GV#MOIDJ t6.4

CHAIN GV#MOIDJ t6.3
~Half.~
EXTERN GV#MOIDJ t6.4

CHAIN GV#MOIDJ t6.4
~I'm sick of pretending. You deserve to know. I'm half duergar. Half of a real dwarf.~
== GV#VYNDJ IF ~IsValidForPartyDialogue("GV#VYND")~ THEN ~...okay, half-right. Good enough.~
== GV#MOIDJ ~My father was a duergar spy, during the war. Don't like to think what happened between him and my mother, but it happened, whatever it was.~
= ~Dunno if she knew the whole time, what she'd be bringing into the world, or if she only realised it when I came out the wrong colour. Dunno which would be worse, honestly. Either way, she decided I wasn't hers.~
= ~Remember I told you my clan leader chose me for his guard? That was why it mattered so much. Me. Half a person. Nothing to something, just like that.~
= ~Everyone advised against it. Everyone. Told him I'd turn round and break his skull the first chance I had. That's why he knew I'd keep quiet, because there was no point talking. Whatever I said against him would've meant nothing. Just hateful slander from greyskin trash.~
END
++ ~Is this why you hide your face?~ EXTERN GV#MOIDJ t6.5
++ ~So where is your father now?~ EXTERN GV#MOIDJ t6.6
++ ~Were you mistreated growing up, then?~ EXTERN GV#MOIDJ t6.7
++ ~There's no such thing as half a person. You're just a person.~ EXTERN GV#MOIDJ t6.8
++ ~Why did you tell me this?~ EXTERN GV#MOIDJ t6.9
++ ~Alright, so you're half-grey. Good to know. Was there anything else?~ EXTERN GV#MOIDJ t6.10
++ ~Is there no end to the secrets with you? If you can't be upfront with me, I think it's time you left.~ EXTERN GV#MOIDJ t6.11


APPEND GV#MOIDJ

IF ~~ t6.5
SAY ~...I don't know. Am I hiding it? We did train to wear armour all day. It was a rare moment I bared my head, even back then. But I did.~
= ~Maybe I don't want to be seen as a duergar. Or maybe I just don't want to be seen.~
++ ~So where is your father now?~ + t6.6
++ ~Were you mistreated growing up, then?~ + t6.7
++ ~There's no such thing as half a person. You're just a person.~ + t6.8
++ ~Why did you tell me this?~ + t6.9
++ ~Is there no end to the secrets with you? If you can't be upfront with me, I think it's time you left.~ + t6.10
END

IF ~~ t6.6
SAY ~No idea. Dead, probably. Could be I killed him myself in the war. Wouldn't know him if I saw him. Wouldn't matter if I did.~
++ ~Is this why you hide your face?~ + t6.5
++ ~Were you mistreated growing up, then?~ + t6.7
++ ~There's no such thing as half a person. You're just a person.~ + t6.8
++ ~Why did you tell me this?~ + t6.9
++ ~Is there no end to the secrets with you? If you can't be upfront with me, I think it's time you left.~ + t6.10
END

IF ~~ t6.7
SAY ~The other children threw their stones, but I got used to that. It was the men and women. They didn't want me to exist, but nor they didn't want a child's blood on their hands. So they pretended I wasn't there. No matter what I did, what I said, they'd look everywhere but at me.~
= ~I hope you never know how it feels to live like that. Just a mistake that nobody wants to be the one to fix.~
++ ~Is this why you hide your face?~ + t6.5
++ ~So where is your father now?~ + t6.6
++ ~There's no such thing as half a person. You're just a person.~ + t6.8
++ ~Why did you tell me this?~ + t6.9
++ ~Is there no end to the secrets with you? If you can't be upfront with me, I think it's time you left.~ + t6.10
END

IF ~~ t6.8
SAY ~Not to my mother. Not to the clan. Not even to the duergar, if I somehow went to them. They'd cut my throat and leave me unburied. And I'd get the same from these people now, if they knew. ~
++ ~Is this why you hide your face?~ + t6.5
++ ~So where is your father now?~ + t6.6
++ ~Were you mistreated growing up, then?~ + t6.7
++ ~Why did you tell me this?~ + t6.9
++ ~Is there no end to the secrets with you? If you can't be upfront with me, I think it's time you left.~ + t6.10
END

IF ~~ t6.9
SAY ~I...~
++ ~What? No answer?~ + t6.11
++ ~We both know this wasn't idle conversation.~ + t6.11
++ ~Come on, spit it out.~ + t6.11
END

IF ~~ t6.10
SAY ~...I understand.~
= ~I don't know what my place is in the world. Maybe I was never meant to have one. I just know it's not with you. Not anymore. You deserve better than me.~
= ~I hope you find it, <CHARNAME>.~
IF ~~ DO ~SetGlobal("GV#MOIDJoined","GLOBAL",0) LeaveParty() EscapeArea()~
EXIT
END

IF ~~ t6.11
SAY ~I just needed to know. I needed to know what you'd think. What you'd say.~
++ ~Did you think it would bother me? You're a worthy companion and I trust you, no matter what's under that helmet. Stop trying to diminish yourself.~ + t6.12
++ ~I barely even know what a duergar is. I've never met one, never dealt with them. It means nothing to me. Nothing's changed. So let's just continue on.~ + t6.12
++ ~I'm not some superstitious idiot. Rumours don't interest me. Facts do. And the fact is, you fight for me and fight well. Continue to do so and we'll have no problems, whatever you are.~ + t6.12
++ ~I think you've hidden one too many things from me, and it'd be best if you went on your way before things get worse.~ + t6.10
++ ~I say go, and don't come back. I won't travel another step with you at my side.~ + t6.10
END

IF ~~ t6.12
SAY ~...thank you.~
= ~Thank you, <CHARNAME>.~
IF ~~ DO ~IncrementGlobal("GV#MOIDTalk","GLOBAL",1) RealSetGlobalTimer("GV#MOIDTimer","GLOBAL",3600)~
EXIT
END



//Talk 7
IF ~Global("GV#MOIDTalk","GLOBAL",14)~ t7
SAY ~Have you ever done something you regretted?~
++ ~Not particularly.~ + t7.1
++ ~Running from Sarevok. I should have stayed with Gorion. ~ + t7.2
++ ~More than I can remember.~ + t7.3
++ ~Drinking so much last night.~ + t7.4
END

IF ~~ t7.1
SAY ~Suppose you're still young.~
IF ~~ + t7.5
END

IF ~~ t7.2
SAY ~You'd have died. I'm not saying I don't understand. Not even saying I wouldn't have done it myself. But you would have died.~
IF ~~ + t7.5
END

IF ~~ t7.3
SAY ~You don't regret it that much if you don't remember it.~
IF ~~ + t7.5
END

IF ~~ t7.4
SAY ~No. Something real. Something that sticks with you beyond the next day.~
IF ~~ + t7.5
END

IF ~~ t7.5
SAY ~It's just...what do you do? If you can't take it back, what else is there?~
++ ~You can't undo bad deeds. You can only do good ones.~ + t7.6
++ ~Nothing. You just have to live with it.~ + t7.7
++ ~Drink to forget?~ + t7.8
++ ~Who cares? Just move on. What's done is done.~ + t7.9
END

IF ~~ t7.6
SAY ~Enough to tip the scales. Problem is, the bad ones weigh more.~
IF ~~ + t7.10
END

IF ~~ t7.7
SAY ~No. I'm done living with it. I'd rather die without it.~
IF ~~ + t7.10
END

IF ~~ t7.8
SAY ~Hah. I'm half-duergar, <CHARNAME>. There'd be droughts before I forgot.~
IF ~~ + t7.10
END

IF ~~ t7.9
SAY ~*I* care. I wouldn't have asked if I didn't care. But you don't have an answer, obviously.~
IF ~~ + t7.10
END

IF ~~ t7.10
SAY ~Maybe...maybe I could go back. To the drow camps. Some of my captives might still be alive. If I could even save *one*...~
++ ~I'd be glad to accompany you.~ + t7.11
++ ~You can't just go and storm the Underdark! That's suicide!~ + t7.12
++ ~Do you really think you could look them in the eye after what you did?~ + t7.13
++ ~Just freeing them isn't enough. You'd have to lead them to safety, help them rebuild their lives. Are you ready for that responsibility?~ + t7.14
END

IF ~~ t7.11
SAY ~No. It has to be me. Just me. I'm not dragging anyone else down.~
++ ~Going into the Underdark alone is suicide, Moidre.~ + t7.12
++ ~If that's how you want it...~ + t7.15
++ ~If you go alone, you'll die. That's all that'll happen. Do you really want to save them, or do you just want to make a noble sacrifice?~ + t7.16
END

IF ~~ t7.12
SAY ~Dying doesn't scare me. Never has. And when I meet the gods, at least I can tell them I tried.~
IF ~~ + t7.17
END

IF ~~ t7.13
SAY ~I don't have to look anyone in the eye. Not since I put this helmet on.~
IF ~~ + t7.17
END

IF ~~ t7.14
SAY ~I'm ready to try. Best anyone can say.~
IF ~~ + t7.17
END

IF ~~ t7.15
SAY ~It is. My mistake, my problem, my fight. Nobody else is suffering because of me.~
IF ~~ + t7.17
END

IF ~~ t7.16
SAY ~But...~
= ~Right. You're right. If I die, I die, but it has to be for something. I need a proper plan.~
IF ~~ + t7.17
END

IF ~~ t7.17
SAY ~I doubt they're still alive, but that won't stop me. Doubt kept me in a rut for years, doing the same thing over and over again. Swearing oaths left and right just for something to hold on to. Holding a shield in front of crooks because everyone's a crook deep down, and holding a shield is all I'm good for.~
= ~I'm done listening to doubt.~
++ ~Is this where we part ways, then?~ + t7.18
++ ~About time. Do what you need to do.~ + t7.18
++ ~Just a minute. You're not done working for me yet.~ + t7.18
END

IF ~~ t7.18
SAY ~I'm not going anywhere, <CHARNAME>. Not just yet. Whatever else happens, we'll finish this first. We'll stop your enemies, because this war of theirs won't mean anything. And...~
= ~And because you deserve to live.~
IF ~~ DO ~SetGlobal("GV#MOIDFriend","GLOBAL",1) IncrementGlobal("GV#MOIDTalk","GLOBAL",1) RealSetGlobalTimer("GV#MOIDTimer","GLOBAL",3600)~
EXIT
END


// Talk 8
IF ~Global("GV#MOIDTalk","GLOBAL",16)~ t8
SAY ~Been doing some thinking, <CHARNAME>. About you. About this battle we're fighting. Lots of things. But mostly about myself. Took a long time, lot of wrestling and soul-searching, but I've finally come to a decision, for better or worse.~
= ~I need a bath.~
++ ~Thank the gods.~ + t8.1
++ ~Yes. You do. You do need a bath. Please take a bath, Moidre. Go and take a bath. Take one. Go.~ + t8.2
++ ~Are you inviting me?~ + t8.3
END

IF ~~ t8.1
SAY ~Yeah yeah. Got any soap? Preferably some you won't want back.~
IF ~~ + t8.4
END

IF ~~ t8.2
SAY ~Alright, alright. Don't rush me. This is big.~
IF ~~ + t8.4
END

IF ~~ t8.3
SAY ~No. If I see you, I drown you. Sod the oath.~
IF ~~ + t8.4
END

IF ~~ t8.4
SAY ~Be the first time I've disarmoured in ages, so promise you won't get hurt while I'm busy. The irony alone would kill me.~
++ ~I'm sure I can manage.~ + t8.5
++ ~I'll gladly take that risk.~ + t8.5
++ ~Anyone shows up, I'll ask if they can wait a few minutes.~ + t8.5
END

IF ~~ t8.5
SAY ~Here goes nothing...~
IF ~~ DO ~IncrementGlobal("GV#MOIDTalk","GLOBAL",1) RealSetGlobalTimer("GV#MOIDTimer","GLOBAL",6) RestParty()~
EXIT
END


// Talk 9, after waking

IF ~Global("GV#MOIDTalk","GLOBAL",18)~ t9
SAY ~Gods. Never worked so hard in my life.~
++ ~How do you feel?~ + t9.1
++ ~No details, please. The nose knows.~ + t9.2
END

IF ~~ t9.1
SAY ~...clean.~
++ ~That's all?~ + t9.3
++ ~And how are you finding this new sensation?~ + t9.4
++ ~Your hesitance speaks volumes.~ + t9.5
END

IF ~~ t9.2
SAY ~Glad to hear it. Let's go.~
IF ~~ DO ~IncrementGlobal("GV#MOIDTalk","GLOBAL",1)  ReallyForceSpellRES("GV#CHAR",Myself)~
EXIT
END

IF ~~ t9.3
SAY ~Refreshed. Tingly.~
IF ~~ + t9.6
END

IF ~~ t9.4
SAY ~Eh. Probably won't hold up long once we're marching again. Sweat builds up quick. Have to make it a habit, I suppose.~
IF ~~ + t9.6
END

IF ~~ t9.5
SAY ~Well what do you feel like after a bath? You've more experience than I do. Dunno why you're asking.~
IF ~~ + t9.6
END

IF ~~ t9.6
SAY ~Bit raw as well, maybe. A lot of dead skin came off.~
++ ~Any chance we could take off that helmet now?~ + t9.7
++ ~That's disgusting and you're disgusting and we're not talking about this anymore. ~ + t9.2
END

IF ~~ t9.7
SAY ~Ask me again when you're safe.~
IF ~~ DO ~IncrementGlobal("GV#MOIDTalk","GLOBAL",1) ReallyForceSpellRES("GV#CHAR",Myself)~
EXIT
END





// Nashkel Mines

IF ~Global("GV#MOIDNashkelTalk","GLOBAL",1)~ t.nashkel
SAY ~That's your end of the bargain, then. I'm here till you say otherwise.~
++ ~What, just like that?~ + t.nashkel1
++ ~I plan to track down these bandits, so you'd better be sure.~ + t.nashkel2
++ ~Glad to have you. Just be warned, it's not going to get less dangerous.~ + t.nashkel2
++ ~I'm saying otherwise now. Time you went on your way.~ + t.nashkel3
++ ~Good. Don't speak until I say otherwise either.~ DO ~SetGlobal("GV#MOIDNashkelTalk","GLOBAL",2) SetGlobal("GV#MOIDShutUp","GLOBAL",1)~ EXIT
END

IF ~~ t.nashkel1
SAY ~That was the deal.~
IF ~~ + t.nashkel4
END

IF ~~ t.nashkel2
SAY ~Makes no difference if you're invading the Nine Hells. I gave the oath.~
IF ~~ + t.nashkel4
END

IF ~~ t.nashkel3
SAY ~You're the boss.~
IF ~~ DO ~SetGlobal("GV#MOIDNashkelTalk","GLOBAL",2) SetGlobal("GV#MOIDJoined","GLOBAL",1) LeaveParty() EscapeArea()~ EXIT
END

IF ~~ t.nashkel4
SAY ~Not like I have much else anyway. Least you did something worthwhile. More than I can say for most clients.~
++ ~If I can do something to help people, I will.~ + t.nashkel5
++ ~Don't start thinking this is some charity mission. I'm doing this for my own sake. ~ + t.nashkel6
++ ~You'll find there's much more you can say for me than most people.~ + t.nashkel7
++ ~And who were these other clients?~ + t.nashkel8
END

IF ~~ t.nashkel5
SAY ~Easily said.~
IF ~~ DO ~SetGlobal("GV#MOIDNashkelTalk","GLOBAL",2)~ EXIT
END

IF ~~ t.nashkel6
SAY ~Everyone's got an agenda. No need to warn me.~
IF ~~ DO ~SetGlobal("GV#MOIDNashkelTalk","GLOBAL",2)~ EXIT
END

IF ~~ t.nashkel7
SAY ~Not necessarily a good thing.~
IF ~~ DO ~SetGlobal("GV#MOIDNashkelTalk","GLOBAL",2)~ EXIT
END

IF ~~ t.nashkel8
SAY ~No-one you'd know, and no-one you'd need to.~
IF ~~ DO ~SetGlobal("GV#MOIDNashkelTalk","GLOBAL",2)~ EXIT
END



// Cloakwood Mines


IF ~Global("GV#MOIDCloakwoodTalk","GLOBAL",1)~ t.cloakwood
SAY ~*sigh* Knew there'd be slaves. Knew it.~
++ ~We'll free them. I promise.~ + t.cloakwood1
++ ~Of course there are. Nobody *wants* to do back-breaking labour for evil organisations.~ + t.cloakwood2
++ ~Does that bother you?~ + t.cloakwood3
++ ~Anyone too weak to seize their freedom deserves slavery.~ + t.cloakwood4
END

IF ~~ t.cloakwood1
SAY ~Don't. Don't make promises. If you really want to do something, you shouldn't need to promise. Just do it.~
IF ~~ + t.cloakwood5
END

IF ~~ t.cloakwood2
SAY ~Nobody wants to do back-breaking labour at all. It's just better than starving.~
IF ~~ + t.cloakwood5
END

IF ~~ t.cloakwood3
SAY ~I don't understand why you need to ask that.~
IF ~~ + t.cloakwood5
END

IF ~~ t.cloakwood4
SAY ~That so? Alright. Let's have you stripped and beaten until you can't stand up. Let's chain you to a wall so long you forget how your arms work. Let's leave you starving alone in the darkness until your brain rots. Then you can look me in the eye and tell me you don't deserve what you can't win back.~
= ~There's no fair fight, you ignorant bastard. You don't have your swords and your magic, or your friends. You don't have anything but the work you're given. That's your world, and it's too small for freedom.~
IF ~~ DO ~SetGlobal("GV#MOIDCloakwoodTalk","GLOBAL",2)~ EXIT
END

IF ~~ t.cloakwood5
SAY ~Free men. That's all I wanted to see in this place. Desperate fools who took any job they could get. But there's no sense buying what you can take, is there? No sense.~
IF ~~ DO ~SetGlobal("GV#MOIDCloakwoodTalk","GLOBAL",2)~ EXIT
END

// Flooded Mines, Slaves Freed

IF ~Global("GV#MOIDFloodedTalk","GLOBAL",1)~ t.flood
SAY ~I'm glad you did that. Helping those slaves, drowning their prison.~
++ ~Anyone would have.~  + t.flood1
++ ~Finally coming around on me, are you?~ + t.flood2
++ ~I didn't do it for your praise.~ + t.flood3
END

IF ~~ t.flood1
SAY ~If you really think that's true, I feel sorry for you. And I envy you. But you're probably just saying it.~
IF ~~ + t.flood4
END

IF ~~ t.flood2
SAY ~Maybe. Don't know your reasons, but it hardly matters. Result's the same.~
IF ~~ + t.flood4
END

IF ~~ t.flood3
SAY ~Then I'm even more glad.~
IF ~~ + t.flood4
END

IF ~~ t.flood4
SAY ~Lost more than their freedom, though. Too bad you can't give the rest back.~
++ ~Perhaps not. But we've given them the chance to find it again.~  + t.flood5
++ ~I can't fix every one of every person's problems, Moidre.~ + t.flood6
++ ~I didn't expect you to care so much.~ + t.flood7
END

IF ~~ t.flood5
SAY ~Not we. You. You did that. I'm just here.~
IF ~~ DO ~SetGlobal("GV#MOIDFloodedTalk","GLOBAL",2) RealSetGlobalTimer("GV#MOIDRepTimer","GLOBAL",3600)~ EXIT
END

IF ~~ t.flood6
SAY ~Don't expect you to. Just saying.~
IF ~~ DO ~SetGlobal("GV#MOIDFloodedTalk","GLOBAL",2) RealSetGlobalTimer("GV#MOIDRepTimer","GLOBAL",3600)~ EXIT
END

IF ~~ t.flood7
SAY ~Not sure I did either.~
IF ~~ DO ~SetGlobal("GV#MOIDFloodedTalk","GLOBAL",2) RealSetGlobalTimer("GV#MOIDRepTimer","GLOBAL",3600)~ EXIT
END


// Flooded Mines, Slaves Drowned

IF ~Global("GV#MOIDDrownedTalk","GLOBAL",1)~ t.drown
SAY ~Why?~
++ ~Why what?~ + t.drown1
++ ~Because.~ + t.drown1
++ ~Be quiet.~ + t.drown1
END

IF ~~ t.drown1
SAY ~Why drown those people? What did they do to you?~
++ ~Those mines had to go, and I wasn't going to spend my hard-earned gold just to spare a few slaves.~ + t.drown2
++ ~It sounded like fun. And it was! Didn't you see that man's face when he realised we lied?~ + t.drown3
++ ~Does it matter?~ + t.drown4
++ ~Why didn't you stop me?~ + t.drown5
END

IF ~~ t.drown2
SAY ~You could have warned them. Give them some kind of chance. What would that have cost you?~
++ ~Time and patience. Those people meant nothing to me, nor should they to you.~ + t.drown3
++ ~The amusement of seeing them all die, obviously.~ + t.drown3
++ ~Does it matter?~ + t.drown4
++ ~If you found it so objectionable, why didn't you stop me?~ + t.drown5
END

IF ~~ t.drown3
SAY ~I've never known a monster so honest.~
++ ~That's right, and you never will. I know what I am, and I have the spine to admit it.~ + t.drown6
++ ~What are you trying to tell me here?~ + t.drown6
++ ~I'm a pragmatist, Moidre. You know damn well there's worse than me out there.~ + t.drown6
++ ~If you found it so objectionable, why didn't you stop me?~ + t.drown5
++ ~If you don't like it, go.~ + t.drown7
END

IF ~~ t.drown4
SAY ~...no.~
= ~It doesn't matter. However you justify it, it won't give those people their lives back.~
IF ~~ + t.drown6
END

IF ~~ t.drown5
SAY ~Would you have listened?~
++ ~No.~ + t.drown8
++ ~Depends on what you said.~ + t.drown8
++ ~I never asked why you didn't convince me. Why didn't you *stop* me?~ + t.drown9
END

IF ~~ t.drown6
SAY ~I'll do what I swore to do, <CHARNAME>. Anyone tries to kill you, I'll fight them. And that's it. Never ask me for anything else.~
IF ~~ DO ~SetGlobal("GV#MOIDUnhappy","GLOBAL",1) SetGlobal("GV#MOIDShutUp","GLOBAL",1) ReallyForceSpellRES("GV#STRE",Myself)~ EXIT
END

IF ~~ t.drown7
SAY ~I have permission?~
++ ~Yes.~ + t.drown10
++ ~Of course you don't. You thought I was serious? Get back in line.~ + t.drown6
END

IF ~~ t.drown8
SAY ~No. You wouldn't have. If you have it in you to do something like that, nothing I said would've made a difference.~
IF ~~ + t.drown11
END

IF ~~ t.drown9
SAY ~I...I made an oath. Guards who turn on their ward are less than worthless. ~
IF ~~ + t.drown11
END

IF ~~ t.drown10
SAY ~You're releasing me from the oath. I don't have to protect you anymore.~
++ ~That's right.~ + t.drown12
++ ~Of course I'm not. You thought I was serious? Get back in line.~ + t.drown6
END

IF ~~ t.drown11
SAY ~Don't try to make this my fault. You did it. Even if I should have stopped you, YOU DID IT.~
++ ~And you're an accomplice. Deal with it.~ + t.drown6
++ ~Whenever you're done whining, let me know.~ + t.drown6
++ ~If it bothers you so much, you can go.~ + t.drown7
END

IF ~~ t.drown12
SAY ~Good.~
IF ~~ DO ~SetGlobal("GV#MOIDJoined","GLOBAL",0) LeaveParty() Enemy() Attack(Player1)~ EXIT
END




// High rep after flooding mines

IF ~Global("GV#MOIDRepTalk","GLOBAL",1)~ t.rep
SAY ~You're making a fair name for yourself.~
++ ~I'm just trying to do what's right.~ + t.rep1
++ ~I couldn't have done it without you.~ + t.rep2
++ ~Do you approve?~ + t.rep3
++ ~It's always better to make friends than enemies.~ + t.rep4
END

IF ~~ t.rep1
SAY ~Not just trying. Succeeding.~
IF ~~ + t.rep5
END

IF ~~ t.rep2
SAY ~Yeah you could've. See it clearer every day. You don't need me.~
IF ~~ + t.rep5
END

IF ~~ t.rep3
SAY ~Getting hard to say I don't.~
IF ~~ + t.rep5
END

IF ~~ t.rep4
SAY ~But you are making enemies. Can't do the right thing these days without stepping on someone's toes.~
IF ~~ + t.rep5
END

IF ~~ t.rep5
SAY ~I thought you just wanted the fame at first. Free drinks and a hero's discount. Been waiting to see what's hiding beneath it all.~
= ~But I keep coming back to what you did for those slaves. Paid out of your own pocket, risked drowning, and I can't figure out what you stood to gain.~
++ ~Why are you always looking for a hidden agenda?~ + t.rep6
++ ~Being able to live with myself is a fair gain.~ + t.rep7
++ ~Gold is heavy. And I always enjoy a good swim.~ + t.rep7
END

IF ~~ t.rep6
SAY ~Spotting danger's part of the job. Nothing's more dangerous than ulterior motives.~
IF ~~ + t.rep7
END

IF ~~ t.rep7
SAY ~<CHARNAME>.~
= ~Can I trust you?~
++ ~Of course you can.~ + t.rep8
++ ~Trust me to do what?~ + t.rep9
++ ~Nope! I break promises like eggs.~ + t.rep10
END

IF ~~ t.rep8
SAY ~Don't answer like it's a small question.~
IF ~~ + t.rep11
END

IF ~~ t.rep9
SAY ~To be what you seem to be.~
IF ~~ + t.rep11
END

IF ~~ t.rep10
SAY ~Don't be flippant. Look at me. ~
IF ~~ + t.rep11
END

IF ~~ t.rep11
SAY ~Words are just words. I know that. But if you say it, really say it, at least I have something to hold you to.~
= ~Can I trust you?~
++ ~I won't disappoint you. You have my word.~ + t.rep12
++ ~I don't know if I can make that kind of promise.~ + t.rep13
++ ~No. You can't.~ + t.rep13
END

IF ~~ t.rep12
SAY ~...alright. Can't ask much more than that.~
IF ~~ + t.rep14
END

IF ~~ t.rep13
SAY ~Then you're honest. Still more than I'd hoped.~
IF ~~ + t.rep14
END

IF ~~ t.rep14
SAY ~I want to believe I'm protecting something good. Just for a little while. Please don't let me down.~
IF ~~ DO ~SetGlobal("GV#MOIDRepTalk","GLOBAL",2) SetGlobal("GV#MOIDHappy","GLOBAL",1) RealSetGlobalTimer("GV#MOIDTimer","GLOBAL",3600)~ EXIT
END

// Reputation drop after flooding mines
IF ~Global("GV#MOIDRepDropTalk","GLOBAL",1)~ t.drop
SAY ~Here it is. Finally. The face behind the mask.~
= ~Had me fooled, <CHARNAME>. You really did. Swore I'd never be fooled again, and you went and made me betray myself. Of all the lying bastards I've worked for, you lie the best.~
++ ~It's just a setback, Moidre. I'll be back to heroics in no time.~ + t.drop1
++ ~It's fine. Just as long as there aren't any other long-lost relatives lurking out there.~ + t.drop2
++ ~How are you feeling about it now?~ + t.drop3
END

IF ~~ t.drop1
SAY ~Yeah. Only temporary, right? Just for now. Just until you get through this. We all have to bend sometimes.~
= ~Heard it all before. Once was enough.~
IF ~~ + t.drop4
END

IF ~~ t.drop2
SAY ~Call it what you like. Doesn't matter. Nothing you say matters anymore.~
IF ~~ + t.drop4
END

IF ~~ t.drop3
SAY ~Nothing. I think you know that. Think that's why you dropped the facade. I'm just a fighter. That's all I am, all I'll ever be. And I can't fight you because I swore I wouldn't.~
IF ~~ + t.drop4
END

IF ~~ t.drop4
SAY ~I'll uphold my oath. It's all I can do. And from now on, it's all I will do. Don't expect anything else.~
IF ~~ DO ~SetGlobal("GV#MOIDRepDropTalk","GLOBAL",2) SetGlobal("GV#MOIDHappy","GLOBAL",0) SetGlobal("GV#MOIDUnhappy","GLOBAL",1) SetGlobal("GV#MOIDShutUp","GLOBAL",1) ReallyForceSpellRES("GV#STRE",Myself)~ EXIT
END



//Approaching Sarevok
IF ~Global("GV#MOIDSarevokTalk","GLOBAL",1) Global("GV#MOIDFriend","GLOBAL",1)~ t.sarevok
SAY ~<CHARNAME>, before we go in there, I need a word.~
++ ~What about?~ + t.sarevok1
++ ~Not now. Let's get this done.~ DO ~SetGlobal("GV#MOIDSarevokTalk","GLOBAL",4)~ EXIT
END

IF ~~ t.sarevok1
SAY ~Come aside. Won't take long.~
= ~There are only two outcomes here. Either I help you win this fight, or I die trying. I'm ready for either one.~
= ~If I do fall, I don't expect you to remember me. Not even sure I want you to. Might be for the best if my time in this world was just forgotten. But you'll remember anyway, I think, whether I want it or not. Whether I expect it or not. So...~
IF ~~ DO ~ClearAllActions() StartCutSceneMode() StartCutScene("GV#CMOID")~ EXIT
END

IF ~Global("GV#MOIDSarevokTalk","GLOBAL",2) Global("GV#MOIDFriend","GLOBAL",1)~ t.sarevok2
SAY ~Remember this. Let the memory be a face, not just a shadow behind a visor.~
++ ~I remember people by their deeds and their character, Moidre. You'll be more than just a face.~ + t.sarevok3
++ ~Come now, no talk of what will happen if we die. This isn't the day.~ + t.sarevok4
++ ~Agh, my memory for faces is terrible. It's so much easier thinking of you as a walking suit of armour.~ + t.sarevok5
+ ~GlobalGT("GV#MOIDTalk","GLOBAL",18)~ + ~First a bath, now the helmet? I half expect to see you singing and dancing at this rate.~ + t.sarevok6
END

IF ~~ t.sarevok3
SAY ~If that's true, I hope I live. I'm not proud of my character or my deeds, <CHARNAME>, but if I have enough time...maybe someday, I can be.~
IF ~~ + t.sarevok7
END

IF ~~ t.sarevok4
SAY ~If I have anything to say about it, no. It isn't.~
IF ~~ + t.sarevok7
END

IF ~~ t.sarevok5
SAY ~That's fair. Most of my life, that's all I've been.~
IF ~~ + t.sarevok7
END

IF ~~ t.sarevok6
SAY ~Not in your lifetime. I'll swear another oath on it.~
IF ~~ + t.sarevok7
END

IF ~~ t.sarevok7
SAY ~How do I...how do I look?~
++ ~Beautiful, in your own way. Although your hair's a disaster.~ + t.sarevok8
++ ~Normal. Just a normal woman. It's a bit disappointing after building up all that mystery.~ + t.sarevok8
++ ~Gods, no wonder you wear that thing all day. You're hideous.~ + t.sarevok8
++ ~I never thought I'd hear that question from you, of all people.~ + t.sarevok9
END

IF ~~ t.sarevok8
SAY ~I'll have to take your word. Don't even remember myself anymore. All I'd see in a mirror is a stranger.~
IF ~~ + t.sarevok10
END

IF ~~ t.sarevok9
SAY ~Just curiosity. It's been so long, I'm not even sure I'd recognise myself anymore. Thought I might as well give myself some idea.~
IF ~~ + t.sarevok10
END

IF ~~ t.sarevok10
SAY ~Doesn't matter.~
IF ~~ DO ~ClearAllActions() StartCutSceneMode() StartCutScene("GV#CMOI1")~ EXIT
END

IF ~Global("GV#MOIDSarevokTalk","GLOBAL",3) Global("GV#MOIDFriend","GLOBAL",1)~ t.sarevok11
SAY ~Won't kill Sarevok with my looks.~
IF ~~ DO ~SetGlobal("GV#MOIDSarevokTalk","GLOBAL",4)~ EXIT
END





//PIDs


IF ~IsGabber(Player1) GlobalGT("GV#MOIDTalk","GLOBAL",2) Global("GV#MOIDUnhappy","GLOBAL",0)~ GV#MOIDPID
SAY ~Hm?~
++ ~How are you?~ + p1.1
+ ~Global("GV#MOIDFriend","GLOBAL",0)~ + ~How do you think we're doing so far?~ + p2.1
+ ~Global("GV#MOIDUnhappy","GLOBAL",0) Global("GV#MOIDFriend","GLOBAL",1)~ + ~How do you think we're doing so far?~ + p2.2
+ ~Global("GV#MOIDPidIron","GLOBAL",0) GlobalLT("CHAPTER","GLOBAL",%tutu_chapter_3%)~ + ~Do you have any ideas as to what's affecting the local iron?~ DO ~SetGlobal("GV#MOIDPidIron","GLOBAL",1)~ + p.iron
+ ~Global("GV#MOIDPidBathe","GLOBAL",0) GlobalLT("GV#MOIDTalk","GLOBAL",18)~ + ~Could I impose on you to bathe?~ DO ~SetGlobal("GV#MOIDPidBathe","GLOBAL",1)~ + p.bathe
+ ~Global("GV#MOIDPidHelmet","GLOBAL",0)~ + ~Don't you ever take that helmet off?~ DO ~SetGlobal("GV#MOIDPidHelmet","GLOBAL",1)~ + p.helmet
+ ~!Race(Player1,DWARF) Global("GV#MOIDPidBeard","GLOBAL",0)~ + ~I can't help noticing you have a beard.~ DO ~SetGlobal("GV#MOIDPidBeard","GLOBAL",1)~ + p.beard
+ ~Race(Player1,DWARF) Global("GV#MOIDPidBeard","GLOBAL",0)~ + ~It's refreshing to see a dwarven woman wear her beard so proudly.~ DO ~SetGlobal("GV#MOIDPidBeard","GLOBAL",1)~ + p.beard2
+ ~Global("GV#MOIDPidMines","GLOBAL",0) GlobalLT("CHAPTER","GLOBAL",%tutu_chapter_3%)~ + ~Why not investigate the mines yourself? You're a strong fighter; you could easily round up a group and take care of it.~ DO ~SetGlobal("GV#MOIDPidMines","GLOBAL",1)~ + p.mines
+ ~Global("GV#MOIDPidMines","GLOBAL",0) GlobalGT("CHAPTER","GLOBAL",%tutu_chapter_2%)~ + ~Why didn't you investigate the mines yourself? You're a strong fighter; you could easily have rounded up a group and taken care of it.~ DO ~SetGlobal("GV#MOIDPidMines","GLOBAL",1)~ + p.mines
+ ~GlobalGT("GV#MOIDTalk","GLOBAL",4) Global("GV#MOIDPidSuggest","GLOBAL",0)~ + ~Any suggestions on what to do next?~ DO ~SetGlobal("GV#MOIDPidSuggest","GLOBAL",1)~ + p.suggest
+ ~GlobalGT("GV#MOIDTalk","GLOBAL",4) Global("GV#MOIDPidGold","GLOBAL",0)~ + ~You didn't have much gold when I met you. Has business been slow?~ DO ~SetGlobal("GV#MOIDPidGold","GLOBAL",1)~ + p.gold
+ ~GlobalGT("GV#MOIDTalk","GLOBAL",4) Global("GV#MOIDPidJobs","GLOBAL",0)~ + ~I'd like to hear more about these past jobs of yours.~ DO ~SetGlobal("GV#MOIDPidJobs","GLOBAL",1)~ + p.jobs
+ ~GlobalGT("GV#MOIDTalk","GLOBAL",6) Global("GV#MOIDPidWhere","GLOBAL",0)~ + ~Where are you from, Moidre?~ DO ~SetGlobal("GV#MOIDPidWhere","GLOBAL",1)~ + p.where
+ ~GlobalGT("GV#MOIDTalk","GLOBAL",6) Global("GV#MOIDPidSullen","GLOBAL",0)~ + ~Were you born sullen or did you grow into it?~ DO ~SetGlobal("GV#MOIDPidSullen","GLOBAL",1)~ + p.sullen
+ ~GlobalGT("GV#MOIDTalk","GLOBAL",6) Global("GV#MOIDPidHair","GLOBAL",0)~ + ~What do you do when you need to cut your hair?~ DO ~SetGlobal("GV#MOIDPidHair","GLOBAL",1)~ + p.hair
+ ~GlobalGT("GV#MOIDTalk","GLOBAL",8) Global("GV#MOIDPidTravel","GLOBAL",0)~ + ~I don't understand why you're still travelling with me.~ DO ~SetGlobal("GV#MOIDPidTravel","GLOBAL",1)~ + p.travel
+ ~GlobalGT("GV#MOIDTalk","GLOBAL",8) Global("GV#MOIDPidHigh","GLOBAL",0)~ + ~You don't seem to think very highly of yourself.~ DO ~SetGlobal("GV#MOIDPidHigh","GLOBAL",1)~ + p.high
+ ~GlobalGT("GV#MOIDTalk","GLOBAL",8) Global("GV#MOIDPidWork","GLOBAL",0)~ + ~What do you do when you're not working?~ DO ~SetGlobal("GV#MOIDPidWork","GLOBAL",1)~ + p.work
+ ~Global("GV#MOIDCloakwoodTalk","GLOBAL",2) Global("GV#MOIDPidSlave","GLOBAL",0)~ + ~Were you a slave?~ DO ~SetGlobal("GV#MOIDPidSlave","GLOBAL",1)~ + p.slave
+ ~Global("GV#MOIDHappy","GLOBAL",1) GlobalGT("CHAPTER","GLOBAL",%tutu_chapter_6%) Global("GV#MOIDPidFist","GLOBAL",0) !Dead("Rieltar")~ + ~Don't you object to fighting the Flaming Fist for me?~ DO ~SetGlobal("GV#MOIDPidFist","GLOBAL",1)~ + p.fist
+ ~Global("GV#MOIDHappy","GLOBAL",1) GlobalGT("CHAPTER","GLOBAL",%tutu_chapter_6%) Global("GV#MOIDPidFist","GLOBAL",0) Dead("Rieltar")~ + ~Don't you object to fighting the Flaming Fist for me?~ DO ~SetGlobal("GV#MOIDPidFist","GLOBAL",1)~ + p.fist1
+ ~Global("GV#MOIDHappy","GLOBAL",1) Global("GV#MOIDPidBounce","GLOBAL",0)~ + ~You've a bit more bounce in your step lately.~ DO ~SetGlobal("GV#MOIDPidBounce","GLOBAL",1)~ + p.bounce
+ ~GlobalGT("GV#MOIDTalk","GLOBAL",10) Global("GV#MOIDPidClan","GLOBAL",0)~ + ~What happened to your clan, in the end?~ DO ~SetGlobal("GV#MOIDPidClan","GLOBAL",1)~ + p.clan
+ ~GlobalGT("GV#MOIDTalk","GLOBAL",12) Global("GV#MOIDPidDuergar","GLOBAL",0)~ + ~So what sets a half-duergar apart from a full one?~ DO ~SetGlobal("GV#MOIDPidDuergar","GLOBAL",1)~ + p.duergar
+ ~GlobalGT("GV#MOIDTalk","GLOBAL",12) Global("GV#MOIDPidLife","GLOBAL",0)~ + ~If you could do absolutely anything else with your life, what would it be?~ DO ~SetGlobal("GV#MOIDPidLife","GLOBAL",1)~ + p.life
+ ~GlobalGT("GV#MOIDTalk","GLOBAL",14) Global("GV#MOIDPidRecognise","GLOBAL",0)~ + ~If you do manage to find those slaves, do you think they'll recognise you?~ DO ~SetGlobal("GV#MOIDPidRecognise","GLOBAL",1)~ + p.recognise
+ ~GlobalGT("GV#MOIDTalk","GLOBAL",18) Global("GV#MOIDPidSoap","GLOBAL",0)~ + ~Can I get my soap back now?~ DO ~SetGlobal("GV#MOIDPidSoap","GLOBAL",1)~ + p.soap
+ ~Global("GV#MOIDHappy","GLOBAL",1) Global("GV#MOIDPidBhaal","GLOBAL",0) PartyHasItem("%tutu_var%SCRL2J")~ + ~Moidre, Gorion's letter said I'm part of Alaundo's prophecy. I'm one of Bhaal's children.~ DO ~SetGlobal("GV#MOIDPidBhaal","GLOBAL",1)~ + p.bhaal
+ ~Global("GV#MOIDPidAjantis","GLOBAL",0) Global("GV#MOIDAjantis2","GLOBAL",1) InParty("Ajantis")~ + ~Those shield lessons you're giving Ajantis. Is that really what dwarves are taught?~ DO ~SetGlobal("GV#MOIDPidAjantis","GLOBAL",1)~ + p.ajantis
+ ~Global("GV#MOIDPidAlora","GLOBAL",0) Global("GV#MOIDAlora3","GLOBAL",1) InParty("Alora")~ + ~I can't believe Alora actually got you to smile.~ DO ~SetGlobal("GV#MOIDPidAlora","GLOBAL",1)~ + p.alora
+ ~Global("GV#MOIDPidImoen","GLOBAL",0) Global("GV#MOIDImoen2","GLOBAL",1) InParty("Imoen")~ + ~Would you really fight your own friends and family for me?~ DO ~SetGlobal("GV#MOIDPidImoen","GLOBAL",1)~ + p.imoen
+ ~Global("GV#MOIDPidJaheira","GLOBAL",0) Global("GV#MOIDJaheira2","GLOBAL",1) InParty("Jaheira")~ + ~That poison rain thing didn't actually happen, did it?~ DO ~SetGlobal("GV#MOIDPidJaheira","GLOBAL",1)~ + p.jaheira
+ ~Global("GV#MOIDPidMontaron","GLOBAL",0) Global("GV#MOIDMontaron1","GLOBAL",1) InParty("Montaron")~ + ~I take it you don't trust Montaron.~ DO ~SetGlobal("GV#MOIDPidMontaron","GLOBAL",1)~ + p.montaron
+ ~Global("GV#MOIDPidMinsc","GLOBAL",0) Global("GV#MOIDMinsc2","GLOBAL",1) InParty("Minsc")~ + ~Don't make Minsc think too hard. It hurts to watch.~  DO ~SetGlobal("GV#MOIDPidMinsc","GLOBAL",1)~+ p.minsc
+ ~Global("GV#MOIDPidNeera","GLOBAL",0) Global("GV#MOIDNeera1","GLOBAL",1) InParty("Neera")~ + ~You didn't need to threaten Neera like that.~  DO ~SetGlobal("GV#MOIDPidNeera","GLOBAL",1)~+ p.neera
+ ~Global("GV#MOIDPidRasaad","GLOBAL",0) Global("GV#MOIDRasaad2","GLOBAL",1) InParty("Rasaad")~ + ~It sounds like Rasaad's making a believer out of you.~ DO ~SetGlobal("GV#MOIDPidRasaad","GLOBAL",1)~ + p.rasaad
+ ~Global("GV#MOIDPidSafana","GLOBAL",0) Global("GV#MOIDSafana2","GLOBAL",1) InParty("Safana")~ + ~That perfume is intoxicating on you.~ DO ~SetGlobal("GV#MOIDPidSafana","GLOBAL",1)~ + p.safana
+ ~Global("GV#MOIDPidSharteel","GLOBAL",0) Global("GV#MOIDSharteel2","GLOBAL",1) InParty("Sharteel")~ + ~Why so sensitive about the beard?~ DO ~SetGlobal("GV#MOIDPidSharteel","GLOBAL",1)~ + p.sharteel
+ ~Global("GV#MOIDPidSkie","GLOBAL",0) Global("GV#MOIDSkie3","GLOBAL",1) InParty("Skie")~ + ~I think Skie's a lost cause.~ DO ~SetGlobal("GV#MOIDPidSkie","GLOBAL",1)~ + p.skie
+ ~Global("GV#MOIDPidViconia","GLOBAL",0) Global("GV#MOIDViconia1","GLOBAL",1) InParty("Viconia")~ + ~Why do you care about Viconia?~ DO ~SetGlobal("GV#MOIDPidViconia","GLOBAL",1)~ + p.viconia
+ ~Global("GV#MOIDPidXan","GLOBAL",0) Global("GV#MOIDXan1","GLOBAL",1) InParty("Xan")~ + ~So much for cheering Xan up, eh?~ DO ~SetGlobal("GV#MOIDPidXan","GLOBAL",1)~ + p.xan
+ ~Global("GV#MOIDPidYeslick","GLOBAL",0) Global("GV#MOIDYeslick3","GLOBAL",1) InParty("Yeslick")~ + ~If "Moidre" is just your clan name, what's your actual name?~ DO ~SetGlobal("GV#MOIDPidYeslick","GLOBAL",1)~ + p.yeslick
+ ~Global("GV#MOIDPidGirdle","GLOBAL",0) Gender("GV#MOID",MALE)~ + ~How's that girdle treating you?~ DO ~SetGlobal("GV#MOIDPidGirdle","GLOBAL",1)~ + p.girdle
+ ~Global("GV#MOIDPidDrink","GLOBAL",0) Global("GV#MOIDHappy","GLOBAL",0) OR(21)
        AreaCheck("%BaldursGateDocks_JopalinsTavern%")
     	AreaCheck("%Beregost_FeldepostsInn_L1%")
     	AreaCheck("%Beregost_JovialJuggler_L1%")
     	AreaCheck("%Beregost_RedSheaf_L1%")
     	AreaCheck("%Beregost_BurningWizard_L1%")
     	AreaCheck("%EBaldursGate_KeexieTavern_L1%")
     	AreaCheck("%EBaldursGate_ElfsongTavern_L1%")
     	AreaCheck("%EBaldursGate_Inn_L1%")
     	AreaCheck("%FriendlyArmInn_L1%")
     	AreaCheck("%Nashkel_Inn%")
     	AreaCheck("%NBaldursGate_ThreeOldKegs_L1%")
     	AreaCheck("%NEBaldursGate_BlushingMermaid_L1%")
     	AreaCheck("%NEBaldursGate_SplurgingSturgeon_L2%")
     	AreaCheck("%NEBaldursGate_SplurgingSturgeon_L1%")
     	AreaCheck("%NWBaldursGate_Helm&Cloak_L1%")
     	AreaCheck("%NWBaldursGate_Tavern_L1%")
     	AreaCheck("%SEBaldursGate_Inn_L1%")
     	AreaCheck("%SEBaldursGate_Blade&Stars_L1%")
     	AreaCheck("%SWBaldursGate_Tavern_L1%")
     	AreaCheck("%SWBaldursGate_YeOldeInn_L1%")
     	AreaCheck("%UlgothsBeard_Inn%")
     	~
        + ~Want a drink, Moidre?~ DO ~SetGlobal("GV#MOIDPidDrink","GLOBAL",1)~ + p.drink

+ ~Global("GV#MOIDPidDrink","GLOBAL",0) Global("GV#MOIDHappy","GLOBAL",1) OR(21)
        AreaCheck("%BaldursGateDocks_JopalinsTavern%")
     	AreaCheck("%Beregost_FeldepostsInn_L1%")
     	AreaCheck("%Beregost_JovialJuggler_L1%")
     	AreaCheck("%Beregost_RedSheaf_L1%")
     	AreaCheck("%Beregost_BurningWizard_L1%")
     	AreaCheck("%EBaldursGate_KeexieTavern_L1%")
     	AreaCheck("%EBaldursGate_ElfsongTavern_L1%")
     	AreaCheck("%EBaldursGate_Inn_L1%")
     	AreaCheck("%FriendlyArmInn_L1%")
     	AreaCheck("%Nashkel_Inn%")
     	AreaCheck("%NBaldursGate_ThreeOldKegs_L1%")
     	AreaCheck("%NEBaldursGate_BlushingMermaid_L1%")
     	AreaCheck("%NEBaldursGate_SplurgingSturgeon_L2%")
     	AreaCheck("%NEBaldursGate_SplurgingSturgeon_L1%")
     	AreaCheck("%NWBaldursGate_Helm&Cloak_L1%")
     	AreaCheck("%NWBaldursGate_Tavern_L1%")
     	AreaCheck("%SEBaldursGate_Inn_L1%")
     	AreaCheck("%SEBaldursGate_Blade&Stars_L1%")
     	AreaCheck("%SWBaldursGate_Tavern_L1%")
     	AreaCheck("%SWBaldursGate_YeOldeInn_L1%")
     	AreaCheck("%UlgothsBeard_Inn%")
     	~
        + ~Want a drink, Moidre?~ DO ~SetGlobal("GV#MOIDPidDrink","GLOBAL",1)~ + p.drink2
++ ~Never mind.~ EXIT
END

IF ~~ p1.1
SAY ~Fine.~
IF ~~ EXIT
END

IF ~~ p2.1
SAY ~You're not dead yet. That's my metric. Doing fine.~
IF ~~ EXIT
END

IF ~~ p2.2
SAY ~Just keep doing what you're doing, <CHARNAME>. I've got your back.~
IF ~~ EXIT
END


IF ~~ p.iron
SAY ~You asking because I'm a dwarf?~
++ ~I wasn't going to say it, but...~ + p.iron1
++ ~It just seemed like you knew something about it.~ + p.iron2
END

IF ~~ p.iron1
SAY ~You're not the first. Live underground, everyone takes you for a mining expert.~
IF ~~ + p.iron2
END

IF ~~ p.iron2
SAY ~It's sabotage. Only answer that makes sense. Someone looking to invade or monopolise. Couldn't say who, though.~
++ ~You think Amn might be involved?~ + p.iron3
++ ~Is there some way to counteract it?~ + p.iron4
++ ~What makes you say that? Maybe the iron in that mine is just going bad.~ + p.iron5
END

IF ~~ p.iron3
SAY ~Dunno. Don't pay attention to politics. Nothing good down that road.~
IF ~~ EXIT
END

IF ~~ p.iron4
SAY ~Stop whoever's doing it. Otherwise, no. Un-corroding the iron would be like trying to un-burn wood.~
IF ~~ EXIT
END

IF ~~ p.iron5
SAY ~It's not a crop. It doesn't get diseases. Only thing that can make iron crumble like that is bad smiths or bad doings.~
IF ~~ EXIT
END

IF ~~ p.bathe
SAY ~Could you impose on me to take off my armour, put down my weapons and leave myself unable to do my job?~
= ~No.~
IF ~~ EXIT
END

IF ~~ p.helmet
SAY ~For what? Luxury? If I'm not on guard, I'm not earning my keep.~
++ ~What keep? I'm not paying you a wage.~ + p.helmet1
++ ~I'm not going to drop dead because you relaxed for a few minutes.~ + p.helmet2
++ ~I order you to remove it.~ + p.helmet3
++ ~It seems like you're hiding something.~ + p.helmet4
END

IF ~~ p.helmet1
SAY ~Habit, then.~
IF ~~ EXIT
END

IF ~~ p.helmet2
SAY ~Maybe. Rather not test it.~
IF ~~ EXIT
END

IF ~~ p.helmet3
SAY ~No. I swore to protect, not obey.~
IF ~~ EXIT
END

IF ~~ p.helmet4
SAY ~My skull. From arrows.~
IF ~~ EXIT
END

IF ~~ p.beard
SAY ~Dwarves have beards.~
++ ~But you're a woman.~ + p.beard1
++ ~Even female dwarves? I didn't know that.~ + p.beard2
END

IF ~~ p.beard1
SAY ~Dwarves have beards.~
IF ~~ EXIT
END

IF ~~ p.beard2
SAY ~Wasn't that uncommon where I came from. When we leave dwarven territory, that's when the razors come out. Most other races can't handle bearded women. Spares you a lot of questions, a lot of awkwardness, and a lot of bad jokes.~
++ ~None of which bothers you, obviously.~ + p.beard3
++ ~Bah. Let them choke on it, I say.~ + p.beard4
++ ~*I* know how to handle a bearded woman.~ + p.beard5
++ ~Speaking of which, have you heard the one about-~ + p.beard6
END

IF ~~ p.beard3
SAY ~Gets annoying sometimes. Just happens less when you're holding a hammer.~
IF ~~ EXIT
END

IF ~~ p.beard4
SAY ~Likewise.~
IF ~~ EXIT
END


IF ~~ p.beard5
SAY ~No.~
IF ~~ EXIT
END


IF ~~ p.beard6
SAY ~Yes.~
IF ~~ EXIT
END



IF ~~ p.mines
SAY ~If I asked someone to follow me into a "demon" infested hole, you think they'd do it?~
++ ~Of course they would.~ + p.mines1
++ ~Maybe if you presented yourself better.~ + p.mines2
++ ~Probably not, no.~ + p.mines3
END

IF ~~ p.mines1
SAY ~You've a bad eye for leadership.~
IF ~~ + p.mines4
END

IF ~~ p.mines2
SAY ~What you see is what you get. Prefer it that way. Nobody gets confused.~
IF ~~ + p.mines4
END

IF ~~ p.mines3
SAY ~Exactly.~
IF ~~ + p.mines4
END

IF ~~ p.mines4
SAY ~Some take orders, some give them. I'm a taker, and I don't play make-believe.~
IF ~~ EXIT
END


IF ~~ p.suggest
SAY ~Nope.~
++ ~Nothing at all?~ + p.suggest1
++ ~ Thank you. Your input is invaluable.~ + p.suggest1
END

IF ~~ p.suggest1
SAY ~You make decisions, I make sure they don't get you killed.~
IF ~~ EXIT
END


IF ~~ p.gold
SAY ~Below average. Like I said, crisis made life more expensive. Smiths charging more. Some think ironclad guards will just make them more of a target.~
= ~Besides, there's food and drink to cover. Mostly drink.~
++ ~How is it possible to drink that much gold away?~ + p.gold1
++ ~You'd be a lot wealthier for a little abstinence.~ + p.gold2
++ ~No long-term plans? No investments?~ + p.gold3
END

IF ~~ p.gold1
SAY ~Don't know many dwarves, do you?~
IF ~~ EXIT
END

IF ~~ p.gold2
SAY ~Wealth to spend on what? Better drink? Nothing worth splurging on in human bars. All just means to the end of making life more bearable.~
IF ~~ EXIT
END

IF ~~ p.gold3
SAY ~Don't like thinking long term. Too depressing. Makes my headache worse.~
IF ~~ EXIT
END

IF ~~ p.speech7
SAY ~When they spoke Common, yeah. Which was mostly just when they were teaching it to me, but still. Go tell them they don't talk like elves. I'm sure that'll go down a treat.~
IF ~~ EXIT
END


IF ~~ p.jobs
SAY ~What for?~
++ ~To get a better measure of who I'm working with.~ + p.jobs1
++ ~It sounds like you've done things I disagree with. I may not be able to travel with you in good conscience.~ + p.jobs2
++ ~I enjoy a good, bloody story.~ + p.jobs3
END

IF ~~ p.jobs1
SAY ~Not much in them to measure.~
IF ~~ + p.jobs4
END

IF ~~ p.jobs2
SAY ~Do you disagree with me fighting for you? Long as I'm here, that's all I'm doing.~
++ ~Even so, I think it'd best to hear about them. It'd give me a better measure of you.~ + p.jobs1
++ ~That's not the point. I can't shelter a criminal.~ + p.jobs2a
++ ~Stop stonewalling and tell me.~ + p.jobs2b
END

IF ~~ p.jobs2a
SAY ~You're not. I'm sheltering you. That's the arrangement. Besides, I could tell you a dozen stories and the most you could accuse me of is standing by.~
IF ~~ + p.jobs4
END

IF ~~ p.jobs2b
SAY ~I'm not stonewalling. Just not much to tell.~
IF ~~ + p.jobs4
END

IF ~~ p.jobs3
SAY ~Won't enjoy these, then.~
IF ~~ + p.jobs4
END

IF ~~ p.jobs4
SAY ~Lot of the time, nothing happens. I'm just a presence. People see me and don't do things they would've done otherwise.~
++ ~So tell me about a time when something did happen.~ + p.jobs5
++ ~That sounds boring.~ + p.jobs6
END

IF ~~ p.jobs5
SAY ~There was a fight.~
++ ~And?~ + p.jobs7
++ ~What kind of fight?~ + p.jobs8
++ ~Did you win?~ + p.jobs9
END

IF ~~ p.jobs6
SAY ~Boring is good. I don't find trouble. I keep it from finding us.~
IF ~~ EXIT
END

IF ~~ p.jobs7
SAY ~Some people died.~
IF ~~ + p.jobs10
END

IF ~~ p.jobs8
SAY ~A short one.~
IF ~~ + p.jobs10
END

IF ~~ p.jobs9
SAY ~Yes.~
IF ~~ + p.jobs10
END

IF ~~ p.jobs10
SAY ~Don't know what you want me to say. Someone attacks my clients, I don't stop to ask who they are.~
++ ~Maybe you should try it.~ + p.jobs11
++ ~You're a very confusing person.~ + p.jobs12
++ ~Well. Since I'm your client now, it's good to know you won't be hesitating.~ + p.jobs13
++ ~Moidre, any mercenary would ask for some kind of information up front. Who they're going to be fighting, how many...~ + p.jobs14
END

IF ~~ p.jobs11
SAY ~Even if they feel like answering, there are three outcomes. They leave, they die, or I die. Doesn't matter in any of them.~
IF ~~ EXIT
END

IF ~~ p.jobs12
SAY ~Or you're not used to simplicity.~
IF ~~ EXIT
END

IF ~~ p.jobs13
SAY ~Right. You want to learn anything about me, let it be that.~
IF ~~ EXIT
END

IF ~~ p.jobs14
SAY ~Did I ask you?~
++ ~...no.~ + p.jobs15
++ ~You should have!~ + p.jobs16
END

IF ~~ p.jobs15
SAY ~Right.~
IF ~~ EXIT
END

IF ~~ p.jobs16
SAY ~But did I?~
IF ~~ EXIT
END


IF ~~ p.where
SAY ~Does it matter?~
++ ~It matters to me.~ + p.where1
++ ~No. I'm just curious.~ + p.where2
END

IF ~~ p.where1
SAY ~No it doesn't. My past isn't what protects you.~
IF ~~ + p.where3
END

IF ~~ p.where2
SAY ~Don't be. Nothing worth curiosity.~
IF ~~ + p.where3
END

IF ~~ p.where3
SAY ~You've watched me fight, and I'm not fighting you. You know what you need to.~
++ ~But how did you become a mercenary? And why?~ + p.where4
++ ~Good relationships aren't formed on a need-to-know basis.~ + p.where5
++ ~Fine. I wasn't that interested anyway.~ EXIT
END

IF ~~ p.where4
SAY ~Because I know how to fight.~
IF ~~ + p.where6
END

IF ~~ p.where5
SAY ~Don't need a good relationship to do my job.~
IF ~~ + p.where6
END

IF ~~ p.where6
SAY ~Ever read a book where they go on and on about the day's weather or the colour of someone's eyes? Ever catch yourself not caring? That's because it's not the story. It's details. Fat.~
= ~My book's got a lot of fat. The meat is, someone starts something, I hit them until it stops. The end.~
++ ~A little fat can add flavour, though.~ + p.where7
++ ~Why are you so guarded?~ + p.where8
++ ~Forget it. I don't know why I bothered.~ + p.where9
END

IF ~~ p.where7
SAY ~Not this fat. Rubber and gristle.~
IF ~~ EXIT
END

IF ~~ p.where8
SAY ~Job.~
IF ~~ EXIT
END

IF ~~ p.where9
SAY ~Me neither.~
IF ~~ EXIT
END

IF ~~ p.sullen
SAY ~Dwarf.~
IF ~~ EXIT
END

IF ~~ p.hair
SAY ~Wait till I'm not on contract. Buy myself a room, sturdy door. Lock it. Barricade it. Check the wardrobe and under the bed. Then a second or two with a sharp knife gets it done. Not like it has to be pretty.~
++ ~How much more neurotic can you get?~ + p.hair1
++ ~Damn. Here I was about to ask you for styling tips.~ + p.hair2
++ ~At that point, why even bother?~ + p.hair3
END

IF ~~ p.hair1
SAY ~Tried an invisibility potion once. Gets harder when you can't see what you're cutting, though. That left a mark.~
IF ~~ EXIT
END

IF ~~ p.hair2
SAY ~No amount of primping survives a helmet. Even I know that.~
IF ~~ EXIT
END

IF ~~ p.hair3
SAY ~When it's growing over your eyes, enough is enough.~
IF ~~ EXIT
END


IF ~~ p.travel
SAY ~People still out for your blood, far as I can tell. Job's not over yet.~
++ ~But I'm not paying you. There's no contract. What's keeping you here?~ + p.travel1
++ ~People could be out for my blood for the rest of my life.~ + p.travel2
++ ~I admire your dedication.~ + p.travel3
END

IF ~~ p.travel1
SAY ~Oath aside? Nothing. But nothing's pushing me away either.~
IF ~~ + p.travel4
END

IF ~~ p.travel2
SAY ~If they are, "the rest of your life" mightn't be long.~
IF ~~ + p.travel4
END

IF ~~ p.travel3
SAY ~Not dedication. Inertia.~
IF ~~ + p.travel4
END
END

CHAIN GV#MOIDJ p.travel4
~My work's all the same. Doesn't matter who it's for. Always the same. Go somewhere, look scary, thump anything that doesn't think I'm scary enough.~
== GV#MOIDJ IF ~ReputationGT(Player1,7)~ THEN ~You haven't asked me to do anything ugly yet. Adventuring covers expenses. Don't fix what isn't broken.~
== GV#MOIDJ IF ~ReputationLT(Player1,8)~ THEN ~You're no worse than my other prospects. Adventuring covers expenses. Don't fix what isn't broken.~
EXIT


APPEND GV#MOIDJ

IF ~~ p.high
SAY ~Should I?~
= ~I'm a decent fighter. Might be proud of that if anything good ever came of it. Otherwise...~
++ ~What comes out of it is entirely up to you.~ + p.high1
++ ~You're an *excellent* fighter.~ + p.high2
++ ~If you worked hard to get good at something, you deserve to be proud. Who cares what comes of it?~ + p.high3
++ ~You're more than that. You're dependable, loyal, honest...~ + p.high4
++ ~Actually, I agree. You're a completely expendable grunt. I just wanted to remind you.~ + p.high5
END

IF ~~ p.high1
SAY ~Not that simple. Just because you think you're doing something doesn't mean you are.~
IF ~~ + p.high6
END

IF ~~ p.high2
SAY ~Could be the best fighter in the world. Makes no difference if the fight's crooked.~
IF ~~ + p.high6
END

IF ~~ p.high3
SAY ~I do. Rather be a bad fighter with a good cause than a good fighter with a bad one. But we don't get to choose what we are.~
IF ~~ + p.high6
END

IF ~~ p.high4
SAY ~Hah. Only because I haven't the sense to be anything else.~
IF ~~ EXIT
END

IF ~~ p.high5
SAY ~Hadn't forgotten.~
IF ~~ EXIT
END

IF ~~ p.high6
SAY ~You might say I'm doing good protecting you, but how would I know? Maybe those people after you are in the right.~
++ ~Do you really believe that?~ + p.high7
+ ~ReputationGT(Player1,13)~ + ~But I've done so many good things!~ + p.high8
+ ~ReputationLT(Player1,7)~ + ~Pfft. What do you mean "maybe"?~ + p.high9
++ ~If I have to worry about you siding with my enemies, you can leave right now.~ + p.high10
END

IF ~~ p.high7
SAY ~Dunno. Only got your word. That's my point.~
IF ~~ EXIT
END

IF ~~ p.high8
SAY ~Since we met. What about before? And why did you do them? No way of telling.~
IF ~~ EXIT
END

IF ~~ p.high9
SAY ~You being wrong doesn't make your enemies right. Sometimes they're just a different kind of wrong.~
IF ~~ EXIT
END

IF ~~ p.high10
SAY ~You don't. I swore an oath. That's what I go by. Only thing I can be sure of.~
IF ~~ EXIT
END

IF ~~ p.work
SAY ~Look for work. Or drink. Can usually do both in the same place, so it works out.~
++ ~What do you *like* to do, then? Apart from drink.~ + p.work1
++ ~Nobody can spend every waking hour either working or finding work. You'd go insane. There must be something else.~ + p.work2
++ ~Your life is dismal, isn't it?~ + p.work3
END

IF ~~ p.work1
SAY ~Not sure I even like that. More of an occupational necessity.~
IF ~~ EXIT
END

IF ~~ p.work2
SAY ~Insanity is doing the same thing over and over again and expecting different results.~
++ ~So?~ + p.work4
END

IF ~~ p.work3
SAY ~Finally noticed?~
IF ~~ EXIT
END

IF ~~ p.work4
SAY ~So maybe I am.~
IF ~~ EXIT
END


IF ~~ p.slave
SAY ~Why?~
++ ~You were pretty upset by what we saw in the Cloakwood mines.~ + p.slave1
END

IF ~~ p.slave1
SAY ~People get upset by lots of things. ~
++ ~You don't.~ + p.slave2
++ ~But you seem to shrug everything else off. Only the slavery bothered you.~ + p.slave3
++ ~Are you being evasive?~ + p.slave4
END

IF ~~ p.slave2
SAY ~I do. You just don't notice.~
IF ~~ + p.slave4
END

IF ~~ p.slave3
SAY ~Shrug what off? The violence? Didn't always. Just got used to it. Show me enough slaves, I'll get used to that too. Not that I want to.~
IF ~~ + p.slave4
END

IF ~~ p.slave4
SAY ~I'm not a slave. Never have been. But I've seen slavery before and it shouldn't happen. That's all.~
IF ~~ EXIT
END

IF ~~ p.fist
SAY ~No. I was there. You didn't do it.~
IF ~~ + p.fist2
END

IF ~~ p.fist1
SAY ~Well. On one hand, you did kill Rieltar. Other hand, what we've seen, he had it coming. Either way, I've got an oath to keep. I'm doing my job, same as them.~
IF ~~ + p.fist2
END

IF ~~ p.fist2
SAY ~I know you don't deserve to die. Could be they don't either, but I'll take definitely over maybe.~
IF ~~ EXIT
END

IF ~~ p.bounce
SAY ~You're seeing things. Nobody bounces in armour, especially not dwarves.~
++ ~You're talking in complete sentences more often too.~ + p.bounce1
++ ~It's subtle, but by your standards you're practically skipping.~ + p.bounce2
++ ~You really shouldn't feed people's prejudices like that.~ + p.bounce3
IF ~~ EXIT
END

IF ~~ p.bounce1
SAY ~Soon fix that.~
IF ~~ EXIT
END

IF ~~ p.bounce2
SAY ~Armour's wearing in, maybe. Always get a bit more spry when it doesn't feel like a weight anymore.~
IF ~~ EXIT
END

IF ~~ p.bounce3
SAY ~Why not? If people assume you're always grumpy, they don't start asking questions when you actually are. "She's just being a dwarf." Saves a lot of time.~
IF ~~ EXIT
END

IF ~~ p.clan
SAY ~We pushed too hard, took too many. Someone was bound to notice. There'd been rumours about where the ore was coming from, and once the people we'd wronged came knocking, they stopped being rumours.~
= ~Half the clan supported our leader. Either didn't believe it or didn't care. Other half revolted. Half and half. Even sides. No winners.~
= ~In the end it was just me, him, and the last of the rebels beating down his door. I turned, yelled at him to run, that I'd hold them off, but he already had a dagger in his own heart.~
= ~They saw me standing over his body, and...I suppose they assumed I'd done it. Probably would have killed me otherwise. Probably would have let them. ~
IF ~~ EXIT
END



IF ~~ p.duergar
SAY ~Can't turn invisible, can't double in size. Probably can't see through illusions either, though I haven't dealt with enough mages to know.~
= ~That, and when a *half* duergar's born, a dwarf clan doesn't put it a sack and take it to the nearest river. Just barely, mind you.~
++ ~And what sets you apart from an everyday dwarf?~ + p.duergar1
++ ~That's a pretty bad deal.~ + p.duergar2
IF ~~ EXIT
END

IF ~~ p.duergar1
SAY ~Suppose I can hold my drink better, but that just means drinking's more expensive. Lightweights don't know how good they have it.~
IF ~~ + p.duergar3
END

IF ~~ p.duergar2
SAY ~Don't I know it.~
IF ~~ + p.duergar3
END

IF ~~ p.duergar3
SAY ~Only saving grace is I don't burn up in the sun. Least, no more than anyone who wears metal all day.~
IF ~~ EXIT
END


IF ~~ p.life
SAY ~...~
= ~Smithing?~
= ~No. Something else. Something without any hammering.~
= ~Gardening.~
++ ~You want to be a *gardener*?~ + p.life1
++ ~Oh. I was hoping for something exciting.~ + p.life2
++ ~What would you grow?~ + p.life3
END

IF ~~ p.life1
SAY ~I want to keep something alive without having to kill anyone to do it.~
IF ~~ + p.life3
END

IF ~~ p.life2
SAY ~What would you call exciting? Travelling? Fighting? Done enough of those.~
IF ~~ + p.life3
END

IF ~~ p.life3
SAY ~Hops, maybe. Get a little brewery going on the side. My own custom ales, for all the dwarves in human lands who can't find a decent drink anymore. Let the first batch go for free, as a toast to lost clans...~
= ~*sigh* Now you've got me thinking about what could have been.~
IF ~~ EXIT
END

IF ~~ p.recognise
SAY ~Only thing they'd remember is the helmet. I'll wear a different one. Don't want them knowing who I am up front, or they won't follow me. But once they're safe, I'll...~
= ~I'll tell them. Everything. They'd call me a hero otherwise, and I couldn't bear that.~
IF ~~ EXIT
END

IF ~~ p.soap
SAY ~You don't want it back. Trust me. I'll buy you a new one.~
IF ~~ EXIT
END

IF ~~ p.solonor2
SAY ~I won't bother preaching, then. But here's a story for you.~
= ~When I was around twelve, I heard about this ritual the priests have. Every turn of the moon, they mark an arrow with Solonor's symbol and shoot it into the sky. "To pierce the firmament and let the light of his teachings shine down on the faithful." Bit poncy, I know, but you have to do these things. So I thought I'd give it a go.~
= ~Only trouble is, whittling on a crossbow bolt is fiddly work. I was afraid if I didn't get it just right, Solonor would chuck the thing back at me and tell me I was rubbish. Must've ruined a dozen shafts trying to do it perfect.~
= ~By the time I'm finished, my hands are all bloody from where I nicked myself, and half the tribe's asked me what the hell I'm playing at. But I did it. And I shot it into the sky, like I was supposed to. And *just* as I did, a little sunray peeked through the clouds, right where I was standing.~
= ~That's the day I started believing.~
IF ~~ DO ~SetGlobal("GV#MOIDPidSolonorChitChat","GLOBAL",1)~ EXIT
END


IF ~~ p.beautiful
SAY ~Have they arse. I look like a boy dragged through a bush. And I don't approve of liars, so don't bother saying different.~
++ ~Nothing wrong with rugged boyishness. Nothing at all.~ + p.beautiful1
++ ~Alright, alright. Just trying to be nice.~ + p.beautiful2
END

IF ~~ p.beautiful1
SAY ~Oh. Well. Thanks, then.~
= ~*cough*~
IF ~~ EXIT
END

IF ~~ p.beautiful2
SAY ~What, and I've got so little respect for myself that I need you to fill in? Piss off. Tell me I have good aim next time. At least that'll be the truth.~
IF ~~ EXIT
END


IF ~~ p.father
SAY ~Oh, just...everything we could fit in a day. What have you been up to, what was my mum like, how'd you end up with that <CHARNAME> prat.~
= ~Heh, he told me he had a hell of a time getting the tribe to warm up to him. He just kept plaguing 'em and plaguing 'em until they were ready to smother him in honey and hang him up for the bears.~
= ~Then he got caught with 'em in a gnoll raid, ended up saving the chieftain's life. And that did it. I'd call it a tall tale, but that's about what it'd take to get a wild elf to trust you. Especially enough to...to take a baby off you.~
IF ~~ EXIT
END

IF ~~ p.mother
SAY ~Well. He said she was like me, and I asked what he meant by that. And he said "Like boiled leather. Rough, tough, doesn't bend easily."~
= ~I said "I didn't think you'd have liked that." And he said "She damned well made me."~
IF ~~ EXIT
END

IF ~~ p.plans
SAY ~I might finish my dad's book. Ain't sure I can write as pretty as he did, but I'll do my best. After that, who knows? I've got nothing tying me down, much like yourself. What do you reckon you'll do?~
++ ~I'll continue adventuring, naturally.~ + p.plans1
++ ~I'll find a home and a way to earn a crust. I really just want a normal life.~ + p.plans2
++ ~It might not be up to me. Something tells me I'm not in for a peaceful future.~ + p.plans3
++ ~I haven't thought about it.~ + p.plans4
END

IF ~~ p.plans1
SAY ~Fair enough, but you won't always be running into problems like this. There comes a time when "adventuring" is just wandering.~
IF ~~ EXIT
END

IF ~~ p.plans2
SAY ~Yeah, I wouldn't blame you. You never asked for any of this.~
IF ~~ + p.plans5
END

IF ~~ p.plans3
SAY ~Nah, things are bound to settle eventually. You keep stomping your enemies the way you are now, one day they'll decide you're best left alone.~
IF ~~ + p.plans5
END

IF ~~ p.plans4
SAY ~Me neither. Not as much as I should've, at least. I always thought I'd live and die with the tribe, but now...~
= ~It's a bit scary, isn't it? Having this much freedom. But it's a blessing, even then. Don't forget that.~
IF ~~ EXIT
END

IF ~~ p.plans5
SAY ~Soon, alright? Soon this'll be over and we'll find somewhere you can put your feet up.~
IF ~~ EXIT
END


IF ~~ p.bhaal
SAY ~Does that terminate our agreement?~
++ ~No, but-~ + p.bhaal1
++ ~Yes.~ + p.bhaal2
END

IF ~~ p.bhaal1
SAY ~Then I didn't need to know it.~
IF ~~ + p.bhaal3
END

IF ~~ p.bhaal2
SAY ~Fine. Let's make a new one.~
= ~Alak minn orn nagh mair. Take my life in your hands. Let me suffer where you would have suffered. Let me bleed where you would have bled. Let me fall where you would have fallen.~
IF ~~ + p.bhaal3
END

IF ~~ p.bhaal3
SAY ~You're the only person I've worked for who's worth a damn, <CHARNAME>. I'm not going anywhere unless that changes.~
IF ~~ EXIT
END

IF ~~ p.drink
SAY ~Always. I'm a dwarf, you know what to get.~
IF ~~ EXIT
END

IF ~~ p.drink2
SAY ~Do you? Think it's time I got some rounds in.~
IF ~~ EXIT
END

IF ~~ p.girdle
SAY ~Girdle?~
IF ~~ EXIT
END


//NPC Specific PIDS


//Ajantis

IF ~~ p.ajantis
SAY ~It's what I was taught. Don't know about everyone else.~
++ ~You're worth more than your shield, Moidre.~ + p.ajantis1
++ ~Well, it certainly worked for you.~ + p.ajantis2
++ ~Could you teach me as well?~ + p.ajantis3
++ ~It all sounds like hogwash to me.~ + p.ajantis4
END

IF ~~ p.ajantis1
SAY ~Am I? If someone switched me with a dwarf-shaped golem, would you notice?~
++ ~Yes. I would.~ + p.ajantis5
++ ~It'd probably be more talkative.~ + p.ajantis6
++ ~I might not, honestly.~ + p.ajantis7
END

IF ~~ p.ajantis2
SAY ~No, it works for other people. That's the point.~
IF ~~ EXIT
END

IF ~~ p.ajantis3
SAY ~No. If you do what I do, it defeats the purpose.~
IF ~~ EXIT
END

IF ~~ p.ajantis4
SAY ~'Course it does. You keep yourself alive, I keep other people alive. Different jobs, different techniques.~
IF ~~ EXIT
END

IF ~~ p.ajantis5
SAY ~Only at mealtime.~
IF ~~ EXIT
END

IF ~~ p.ajantis6
SAY ~Eh.~
IF ~~ EXIT
END

IF ~~ p.ajantis7
SAY ~Right. My line of work, it doesn't matter who you are. Just how you fight.~
IF ~~ EXIT
END



//Alora

IF ~~ p.alora
SAY ~Ehh. Throw enough crap at a wall, something'll stick.~
= ~Besides, she had an advantage. Nobody's ever tried to do it before..~
IF ~~ EXIT
END


//Imoen

IF ~~ p.imoen
SAY ~Yes. Not that it makes a difference anymore.~
IF ~~ EXIT
END


//Jaheira

IF ~~ p.jaheira
SAY ~Not literally. Don't rule it out, though. Mages are tricky.~
IF ~~ EXIT
END


//Minsc

IF ~~ p.minsc
SAY ~Gonna hurt even more if he has to make a sacrifice he's not prepared for. Not everything always works out for the best. In fact, it usually doesn't. Best break him in gently.~
IF ~~ EXIT
END


//Montaron

IF ~~ p.montaron
SAY ~Don't trust much of anyone. He just happened to bring it up.~
= ~I'd say boot him, but if you've any sense, you've already thought about it. ~
++ ~He's treacherous, but he's not stupid. He won't turn on us as long as our goals are the same.~ + p.montaron1
++ ~I'd rather have him where I can see him.~ + p.montaron2
++ ~So far I've got no more reason to suspect him than you.~ + p.montaron3
++ ~I haven't, actually.~ + p.montaron4
END

IF ~~ p.montaron1
SAY ~Sooner or later they won't be the same. Wouldn't count on him telling you when.~
IF ~~ EXIT
END

IF ~~ p.montaron2
SAY ~I don't need to know the reason. Long as you have one.~
IF ~~ EXIT
END

IF ~~ p.montaron3
SAY ~Wrong. I might be working for someone else. He's definitely working for someone else.~
IF ~~ EXIT
END

IF ~~ p.montaron4
SAY ~Do. I've got eyes in the back of my head, but the helmet's in the way.~
IF ~~ EXIT
END


//neera

IF ~~ p.neera
SAY ~Not threatening. Warning.~
= ~I don't want to kill her. That's why I told her, so she'd know to run.~
IF ~~ EXIT
END


//Rasaad

IF ~~ p.rasaad
SAY ~Eh. Still dubious.~
= ~"None who would embrace Selune are turned away." Pretty words, but nothing's tested them yet.  He had one good thing happen to him and forgot how many bastards there are in the world.~
IF ~~ EXIT
END


//Safana

IF ~~ p.safana
SAY ~...~
= ~Joking, sincere, don't care. Ignoring it.~
IF ~~ EXIT
END

//Sharteel

IF ~~ p.sharteel
SAY ~Back where I lived, one of the ollams said there were two things every dwarf should have a right to. The hair on their chins, and the support of their clan.~
= ~I've only got one. Have to hold on to it.~
IF ~~ EXIT
END

//Skie

IF ~~ p.skie
SAY ~No doubt. But she'll find out eventually. Then she'll know to listen next time.~
IF ~~ EXIT
END


//Viconia

IF ~~ p.viconia
SAY ~I know what it's like to feel you don't have a place.~
= ~She might deserve a little help. Might not. Dunno what she did before she came topside. I do know I wouldn't wish that feeling on most people, and she's not made herself an exception yet.~
IF ~~ EXIT
END

//Xan

IF ~~ p.xan
SAY ~Nah. Jostled something, I think. Went almost a whole evening afterwards without saying the word "doomed." New record for him.~
IF ~~ EXIT
END

//Yeslick

IF ~~ p.yeslick
SAY ~Not important. You just need something to call me. You won't meet another Moidre, so Moidre will do.~
IF ~~ EXIT
END





// Unhappy PIDs


IF ~IsGabber(Player1) GlobalGT("GV#MOIDTalk","GLOBAL",2) Global("GV#MOIDUnhappy","GLOBAL",1)~ GV#MOIDPIDUnhappy
SAY ~...~
++ ~How are you?~ + p1.1
++ ~What do you think we should do next?~ + p2.unhappy
++ ~Still sulking, I see.~ + p.sulk
++ ~I can't help noticing you're not fighting as hard.~ + p.fight
++ ~Never mind.~ EXIT
END

IF ~~ p2.unhappy
SAY ~Don't care.~
IF ~~ EXIT
END

IF ~~ p.sulk
SAY ~Is there a fight?~
++ ~Not right at this moment, no.~ + p.sulk1
++ ~There's no reason we can't still be friends.~ + p.sulk2
++ ~There might be if this keeps up.~ + p.sulk3
END

IF ~~ p.sulk1
SAY ~Nothing to talk about, then.~
IF ~~ EXIT
END

IF ~~ p.sulk2
SAY ~"Still."~
IF ~~ EXIT
END

IF ~~ p.sulk3
SAY ~I'll be ready for it.~
IF ~~ EXIT
END


IF ~~ p.fight
SAY ~Yeah.~
++ ~May I ask why?~ + p.fight1
++ ~Perhaps we could do something about that?~ + p.fight2
++ ~Get your act together or you're gone.~ + p.fight3
END

IF ~~ p.fight1
SAY ~I'm tired.~
IF ~~ EXIT
END

IF ~~ p.fight2
SAY ~You can't.~
IF ~~ EXIT
END

IF ~~ p.fight3
SAY ~Bad incentive.~
IF ~~ EXIT
END
END