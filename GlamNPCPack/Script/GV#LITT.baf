IF
HotKey(K)
THEN
RESPONSE #100
StartCutSceneMode()
StartCutScene("GV#LITTF")
END


//Introduction
IF
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID)
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
Global("GV#LITTMet","GLOBAL",0)
THEN
RESPONSE #100
StartDialogueNoSet(Player1)
END

//Leave if Fortress complete without her
IF
AreaCheck("%BearRiver%")
GlobalGT("HelpMinsc","GLOBAL",1)
!InParty(Myself)
Global("GV#LITTRecruited","GLOBAL",0)
Global("GV#LITTGone","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("GV#LITTGone","GLOBAL",1)
DestroySelf()
END

//Gnoll Timer Expired
IF
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID)
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
!AreaCheck("%GnollStronghold%")
!AreaCheck("%GnollStronghold_XvartCave%")
!AreaCheck("%GnollStronghold_TomeCave%")
!AreaCheck("%GnollStronghold_CrawlerCave%")
GlobalLT("HelpMinsc","GLOBAL",2)
InParty(Myself)
Global("GV#LITTRecruited","GLOBAL",0)
Global("GV#LITTFedUp","GLOBAL",0)
GlobalTimerExpired("GV#LITTGnollsTimer","GLOBAL")
THEN
RESPONSE #100
SetGlobal("GV#LITTFedUp","GLOBAL",1)
END

IF
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID)
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
!AreaCheck("%GnollStronghold%")
!AreaCheck("%GnollStronghold_XvartCave%")
!AreaCheck("%GnollStronghold_TomeCave%")
!AreaCheck("%GnollStronghold_CrawlerCave%")
GlobalLT("HelpMinsc","GLOBAL",2)
InParty(Myself)
Global("GV#LITTRecruited","GLOBAL",0)
Global("GV#LITTFedUp","GLOBAL",1)
GlobalTimerExpired("GV#LITTGnollsTimer","GLOBAL")
THEN
RESPONSE #100
StartDialogueNoSet(Player1)
END

// Gnarl and Hairtooth
IF
InParty(Myself)
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID)
!StateCheck(Myself,CD_STATE_NOTVALID)
!Dead("Gnarl")
!Dead("Hairtooth")
InMyArea("Gnarl")
InMyArea("Hairtooth")
Global("GV#LITTGnarl","GLOBAL",0)
GlobalLT("HelpMinsc","GLOBAL",2)
THEN
RESPONSE #100
SetGlobal("GV#LITTGnarl","GLOBAL",1)
StartDialogueNoSet(Player1)
END


//Friendship Talk Loop

IF
InParty(Myself)
Global("GV#LITTTalk","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("GV#LITTTalk","GLOBAL",1)
END

IF
InParty(Myself)
RealGlobalTimerExpired("GV#LITTTimer","GLOBAL")
Global("GV#LITTShutUp","GLOBAL",0)
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID)
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
!AreaType(DUNGEON)
OR(2)
Global("GV#LITTTalk","GLOBAL",5)
Global("GV#LITTTalk","GLOBAL",7)
THEN
RESPONSE #100
IncrementGlobal("GV#LITTTalk","GLOBAL",1)
END


IF
InParty(Myself)
Global("GV#LITTShutUp","GLOBAL",0)
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
!AreaType(DUNGEON)
OR(10)
Global("GV#LITTTalk","GLOBAL",2)
Global("GV#LITTTalk","GLOBAL",4)
Global("GV#LITTTalk","GLOBAL",6)
Global("GV#LITTTalk","GLOBAL",8)
Global("GV#LITTTalk","GLOBAL",10)
THEN
RESPONSE #100
StartDialogueNoSet(Player1)
END


//After Dynaheir Rescue
IF
InParty(Myself)
See(Player1)
!StateCheck(Myself,CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
GlobalGT("HelpMinsc","GLOBAL",1)
Global("GV#LITTTalk","GLOBAL",1)
THEN
RESPONSE #100
IncrementGlobal("GV#LITTTalk","GLOBAL",1)
END

//After leaving fortress
IF
InParty(Myself)
See(Player1)
!AreaCheck("%GnollStronghold%")
!AreaCheck("%GnollStronghold_XvartCave%")
!AreaCheck("%GnollStronghold_TomeCave%")
!AreaCheck("%GnollStronghold_CrawlerCave%")
!StateCheck(Myself,CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
Global("GV#LITTTalk","GLOBAL",3)
THEN
RESPONSE #100
IncrementGlobal("GV#LITTTalk","GLOBAL",1)
END

//Nashkel Mines
IF
InParty(Myself)
See(Player1)
AreaCheck("%NashkelMines_L3%")
!StateCheck(Myself,CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
Global("GV#LITTShutUp","GLOBAL",0)
CombatCounter(0)
!See([ENEMY])
Global("GV#LITTNashkelTalk","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("GV#LITTNashkelTalk","GLOBAL",1)
END

IF
InParty(Myself)
See(Player1)
AreaCheck("%NashkelMines_L3%")
!StateCheck(Myself,CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
Global("GV#LITTShutUp","GLOBAL",0)
CombatCounter(0)
!See([ENEMY])
Global("GV#LITTNashkelTalk","GLOBAL",1)
THEN
RESPONSE #100
StartDialogueNoSet(Player1)
END


//After Davaeorn
IF
InParty(Myself)
See(Player1)
Dead("Davaeorn")
OR (4)
AreaCheck("%CloakwoodMines_L1%")
AreaCheck("%CloakwoodMines_L2%")
AreaCheck("%CloakwoodMines_L3%")
AreaCheck("%CloakwoodMines_L4%")
Global("GV#LITTShutUp","GLOBAL",0)
!StateCheck(Myself,CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
Global("GV#LITTDavaeornTalk","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("GV#LITTDavaeornTalk","GLOBAL",1)
END

IF
InParty(Myself)
See(Player1)
Dead("Davaeorn")
OR (4)
AreaCheck("%CloakwoodMines_L1%")
AreaCheck("%CloakwoodMines_L2%")
AreaCheck("%CloakwoodMines_L3%")
AreaCheck("%CloakwoodMines_L4%")
Global("GV#LITTShutUp","GLOBAL",0)
!StateCheck(Myself,CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
Global("GV#LITTDavaeornTalk","GLOBAL",1)
THEN
RESPONSE #100
StartDialogueNoSet(Player1)
END


//First time in Baldur's Gate
IF
InParty(Myself)
See(Player1)
OR (9) AreaCheck("%CentralBaldursGate%")
AreaCheck("%EBaldursGate%")
AreaCheck("%SEBaldursGate%")
AreaCheck("%BaldursGateDocks%")
AreaCheck("%SWBaldursGate%")
AreaCheck("%WBaldursGate%")
AreaCheck("%NWBaldursGate%")
AreaCheck("%NBaldursGate%")
AreaCheck("%NEBaldursGate%")
Global("GV#LITTShutUp","GLOBAL",0)
!StateCheck(Myself,CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
Global("GV#LITTBaldursTalk","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("GV#LITTBaldursTalk","GLOBAL",1)
END

IF
InParty(Myself)
See(Player1)
OR (9) AreaCheck("%CentralBaldursGate%")
AreaCheck("%EBaldursGate%")
AreaCheck("%SEBaldursGate%")
AreaCheck("%BaldursGateDocks%")
AreaCheck("%SWBaldursGate%")
AreaCheck("%WBaldursGate%")
AreaCheck("%NWBaldursGate%")
AreaCheck("%NBaldursGate%")
AreaCheck("%NEBaldursGate%")
Global("GV#LITTShutUp","GLOBAL",0)
!StateCheck(Myself,CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
Global("GV#LITTBaldursTalk","GLOBAL",1)
THEN
RESPONSE #100
StartDialogueNoSet(Player1)
END


//Candlekeep
IF
InParty(Myself)
See(Player1)
AreaCheck("%Candlekeep_Ch6%")
!StateCheck(Myself,CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
Global("GV#LITTShutUp","GLOBAL",0)
Global("GV#LITTCandlekeepTalk","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("GV#LITTCandlekeepTalk","GLOBAL",1)
END

IF
InParty(Myself)
See(Player1)
AreaCheck("%Candlekeep_Ch6%")
!StateCheck(Myself,CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
Global("GV#LITTShutUp","GLOBAL",0)
Global("GV#LITTCandlekeepTalk","GLOBAL",1)
THEN
RESPONSE #100
StartDialogueNoSet(Player1)
END

//Met Dad
IF
InParty(Myself)
See(Player1)
!StateCheck(Myself,CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
Global("GV#LITTShutUp","GLOBAL",0)
Global("GV#LITTDadQuest","GLOBAL",1)
THEN
RESPONSE #100
StartDialogueNoSet(Player1)
END

//After spending time with dad
IF
InParty(Myself)
See(Player1)
!StateCheck(Myself,CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
Global("GV#LITTShutUp","GLOBAL",0)
Global("GV#LITTDadQuest","GLOBAL",5)
Global("GV#LITTThanks","GLOBAL",0)
RealGlobalTimerExpired("GV#LITTDadTalkTimer","GLOBAL")
THEN
RESPONSE #100
SetGlobal("GV#LITTThanks","GLOBAL",1)
END

IF
InParty(Myself)
See(Player1)
!StateCheck(Myself,CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
Global("GV#LITTShutUp","GLOBAL",0)
Global("GV#LITTDadQuest","GLOBAL",5)
RealGlobalTimerExpired("GV#LITTDadTalkTimer","GLOBAL")
Global("GV#LITTThanks","GLOBAL",1)
THEN
RESPONSE #100
StartDialogueNoSet(Player1)
END

//Doppelgangers
IF
InParty(Myself)
See(Player1)
!StateCheck(Myself,CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
Global("GV#LITTShutUp","GLOBAL",0)
AreaCheck("%Candlekeep_Catacombs_L2%")
Global("GV#LITTDoppelgangers","GLOBAL",0)
Global("GV#LITTDoppelgangersTimerSet","GLOBAL",0)
THEN
RESPONSE #100
RealSetGlobalTimer("GV#LITTDoppelgangersTimer","GLOBAL",8)
SetGlobal("GV#LITTDoppelgangersTimerSet","GLOBAL",1)
END

IF
InParty(Myself)
See(Player1)
!StateCheck(Myself,CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
Global("GV#LITTShutUp","GLOBAL",0)
AreaCheck("%Candlekeep_Catacombs_L2%")
Global("GV#LITTDoppelgangers","GLOBAL",0)
RealGlobalTimerExpired("GV#LITTDoppelgangersTimer","GLOBAL")
THEN
RESPONSE #100
SetGlobal("GV#LITTDoppelgangers","GLOBAL",1)
END

IF
InParty(Myself)
See(Player1)
!StateCheck(Myself,CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
Global("GV#LITTShutUp","GLOBAL",0)
AreaCheck("%Candlekeep_Catacombs_L2%")
Global("GV#LITTDoppelgangers","GLOBAL",1)
THEN
RESPONSE #100
StartDialogueNoSet(Player1)
END



//Last talk, before final battle

IF
InParty(Myself)
Global("GV#LITTShutUp","GLOBAL",0)
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID)
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
AreaCheck("%Undercity%")
Global("GV#LITTSarevokTalk","GLOBAL",0)
Global("GV#LITTFriend","GLOBAL",1)
THEN
RESPONSE #100
SetGlobal("GV#LITTSarevokTalk","GLOBAL",1)
END

IF
InParty(Myself)
Global("GV#LITTShutUp","GLOBAL",0)
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID)
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
Global("GV#LITTSarevokTalk","GLOBAL",1)
THEN
RESPONSE #100
StartDialogueNoSet(Player1)
END


//Second Coran banter, after waking
IF
InParty(Myself)
InParty("Coran")
Global("GV#LITTCoran1","GLOBAL",1)
Global("GV#LITTCoran2","GLOBAL",1)
Global("GV#LITTCoran3","GLOBAL",0)
Global("GV#LITTCoranWakeTimerSet","GLOBAL",0)
!StateCheck("Coran",CD_STATE_NOTVALID)
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
THEN
RESPONSE #100
SetGlobal("GV#LITTCoranWakeTimerSet","GLOBAL",1)
RealSetGlobalTimer("GV#LITTCoranWakeTimer","GLOBAL",4)
END

IF
InParty(Myself)
InParty("Coran")
Global("GV#LITTCoran1","GLOBAL",1)
Global("GV#LITTCoran2","GLOBAL",1)
Global("GV#LITTCoran3","GLOBAL",0)
RealGlobalTimerExpired("GV#LITTCoranWakeTimer","GLOBAL")
!StateCheck("Coran",CD_STATE_NOTVALID)
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
THEN
RESPONSE #100
Interact("Coran")
END


//Low Reputation
IF
InParty(Myself)
!StateCheck(Player1,CD_STATE_NOTVALID)
!StateCheck(Myself,CD_STATE_NOTVALID)
ReputationLT(Player1,8)
Global("GV#LITTLowRep","GLOBAL",0)
GlobalGT("GV#LITTTalk","GLOBAL",4)
CombatCounter(0)
!See([ENEMY])
THEN
RESPONSE #100
SetGlobal("GV#LITTLowRep","GLOBAL",1)
END

IF
InParty(Myself)
!StateCheck(Player1,CD_STATE_NOTVALID)
!StateCheck(Myself,CD_STATE_NOTVALID)
ReputationLT(Player1,8)
GlobalGT("GV#LITTTalk","GLOBAL",4)
Global("GV#LITTLowRep","GLOBAL",1)
CombatCounter(0)
!See([ENEMY])
THEN
RESPONSE #100
StartDialogueNoSet(Player1)
END

//Reputation Break
IF
InParty(Myself)
!StateCheck(Player1,CD_STATE_NOTVALID)
!StateCheck(Myself,CD_STATE_NOTVALID)
ReputationLT(Player1,5)
GlobalGT("GV#LITTTalk","GLOBAL",4)
Global("GV#LITTRepBreak","GLOBAL",0)
CombatCounter(0)
!See([ENEMY])
THEN
RESPONSE #100
SetGlobal("GV#LITTRepBreak","GLOBAL",1)
END

IF
InParty(Myself)
!StateCheck(Player1,CD_STATE_NOTVALID)
!StateCheck(Myself,CD_STATE_NOTVALID)
ReputationLT(Player1,8)
GlobalGT("GV#LITTTalk","GLOBAL",4)
Global("GV#LITTRepBreak","GLOBAL",1)
CombatCounter(0)
!See([ENEMY])
THEN
RESPONSE #100
StartDialogueNoSet(Player1)
END


//Neb
IF
InParty(Myself)
!StateCheck(Player1,CD_STATE_NOTVALID)
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
!AreaCheck("%SWBaldursGate_FlamingFistHQ_L1%")
Global("GV#LITTNeb","GLOBAL",1)
THEN
RESPONSE #100
StartDialogueNoSet(Player1)
END


//Sonner Interjection
IF
InParty(Myself)
!StateCheck(Player1,CD_STATE_NOTVALID)
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
Global("GV#LITTSonnerShot","GLOBAL",1)
THEN
RESPONSE #100
StartDialogueNoSet(Player1)
END


//Scalping
IF
InParty(Myself)
!StateCheck(Myself,CD_STATE_NOTVALID)
InParty("GV#VYND")
!StateCheck("GV#VYND",CD_STATE_NOTVALID)
InParty("GV#DAVE")
!StateCheck("GV#DAVE",CD_STATE_NOTVALID)
Global("GV#VaiGone","GLOBAL",2)
Global("GV#LITTScalping","GLOBAL",0)
See("Bandit")
THEN
RESPONSE #100
SetGlobal("GV#LITTScalping","GLOBAL",1)
END

IF
InParty(Myself)
!StateCheck(Myself,CD_STATE_NOTVALID)
InParty("GV#VYND")
See("GV#VYND")
!StateCheck("GV#VYND",CD_STATE_NOTVALID)
InParty("GV#DAVE")
!StateCheck("GV#DAVE",CD_STATE_NOTVALID)
See("GV#DAVE")
Global("GV#VaiGone","GLOBAL",2)
Global("GV#LITTScalping","GLOBAL",1)
CombatCounter(0)
!See([ENEMY])
THEN
RESPONSE #100
StartDialogueNoSet("GV#VYND")
END


//Tenya Killed
IF
InParty(Myself)
!StateCheck(Myself,CD_STATE_NOTVALID)
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
Dead("Tenya")
!Global("HelpJebadoh","GLOBAL",0)
Global("GV#LITTTenya","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("GV#LITTTenya","GLOBAL",1)
END

IF
InParty(Myself)
!StateCheck(Myself,CD_STATE_NOTVALID)
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
Dead("Tenya")
!Global("HelpJebadoh","GLOBAL",0)
Global("GV#LITTTenya","GLOBAL",1)
THEN
RESPONSE #100
StartDialogueNoSet(Player1)
END

//Cloudpeaks Dryad killed
IF
InParty(Myself)
!StateCheck(Myself,CD_STATE_NOTVALID)
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
Dead("CDRYAD")
Global("GV#LITTDryad","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("GV#LITTDryad","GLOBAL",1)
END

IF
InParty(Myself)
!StateCheck(Myself,CD_STATE_NOTVALID)
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
Dead("CDRYAD")
Global("GV#LITTDryad","GLOBAL",1)
THEN
RESPONSE #100
StartDialogueNoSet(Player1)
END